grf{
	grfid                  : "RTNA" ;
	name                   : string(STR_GRF_NAME) ;
	desc                   : string(STR_GRF_DESC) ;
	version                : 0 ;
	min_compatible_version : 0 ;
}

//Cargo table

cargotable {
//Default cargos
   PASS, MAIL, COAL, FOOD, GOLD, GOOD, GRAI, IORE, LVST, OIL_, PAPR, STEL, VALU, WHEA, WOOD,

//Tropical cargos
   CORE, DIAM, FRUT, MAIZ, RUBR, WATR, 

//ECS
   AORE, BDMT, BRCK, CERA, CERE, CMNT, DYES, FERT, FICR, FISH, GLAS, LIME, OLSD, PETR, PLAS, POTA, RFPR, SAND, SULP, TOUR, VEHI, WDPR, WOOL, 
   
//FIRS
   BEER, CLAY, ENSP, FMSP, FRVG, GRVL, MILK, MNSP, PHOS, PORE, RCYC, SCMT, SGBT, SGCN, JAVA, COPR, SUGR,
   
//Nuclear stuff
	URAN, NWST
}




//Nuclear waste cargo

spriteset(gfx_nwst_cargo_icon, "gfx/nuclear_reprocessing_site.png"){[69, 186, 10, 10, 0, 0]}

item(FEAT_CARGOS, cargo_nuclear_waste, 31){
	property{
		number                    : 31 ;
		type_name                 : string(STR_NWST_NAME) ;
		unit_name                 : string(STR_NWST_NAME) ;
		units_of_cargo            : TTD_STR_TONS ;
		items_of_cargo            : string(STR_NWST_ITEMS_OF_CARGO) ;
		type_abbreviation         : string(STR_NWST_ABBREV) ;
		sprite                    : NEW_CARGO_SPRITE ;
		weight                    : 5 ;
		penalty_lowerbound        : 255 ;
		single_penalty_length     : 255 ;
		price_factor              : 30000 ;
		station_list_colour       : 239 ;
		cargo_payment_list_colour : 239 ;
		is_freight                : 1 ;
		cargo_classes             : bitmask(CC_HAZARDOUS) ;
		cargo_label               : "NWST" ;
		capacity_multiplier       : 10 ;
	}
	graphics{
		default : gfx_nwst_cargo_icon ;
	}
}






//Power plant modification

produce(produce_power_plant, 100, 0, 0, 2, 0);
produce(produce_power_plant_arrival, 9999, 0, 0, 0, 0);

item(FEAT_INDUSTRIES, power_plant, 0x01){
	property {
		substitute          : 0x01 ;
		override            : 0x01 ;
		life_type           : IND_LIFE_TYPE_BLACK_HOLE ;
		accept_cargo_types  : [cargotype("COAL")];
		prod_cargo_types    : [cargotype("NWST")] ;
		nearby_station_name : string(STR_POWER_STATION_STATION_NAME) ;
	}	
	graphics {
		produce_cargo_arrival : produce_power_plant_arrival ;
		produce_256_ticks     : produce_power_plant ;
	}
}










//Graphics

spriteset(gfx_nucrep_empty_tile       , "gfx/nuclear_reprocessing_site.png"){[ 0  , 208, 64, 40, -31, -9  ]}
spriteset(gfx_nucrep_empty_tile_loaded, "gfx/nuclear_reprocessing_site.png"){[ 66 , 208, 64, 40, -31, -10  ]}
spriteset(gfx_nucrep_forklift         , "gfx/nuclear_reprocessing_site.png"){[ 156, 124, 64, 41, -31, -9  ]}
spriteset(gfx_nucrep_forklift_loaded  , "gfx/nuclear_reprocessing_site.png"){[ 156, 167, 64, 41, -31, -10  ]}
spriteset(gfx_nucrep_overpass         , "gfx/nuclear_reprocessing_site.png"){[ 69 , 124, 85, 60, -31, -29 ]}
spriteset(gfx_nucrep_shed             , "gfx/nuclear_reprocessing_site.png"){[ 0  , 155, 67, 51, -32, -20 ]}
spriteset(gfx_nucrep_sphere_tank      , "gfx/nuclear_reprocessing_site.png"){[ 66 , 58 , 64, 64, -31, -33 ]}
spriteset(gfx_nucrep_road_overlay     , "gfx/nuclear_reprocessing_site.png"){[ 111, 0  , 96, 47, -31, -16 ]}
spriteset(gfx_nucrep_cooling_tower    , "gfx/nuclear_reprocessing_site.png"){[ 66 , 0  , 43, 56, -21, -32 ]}
spriteset(gfx_nucrep_chimney_building , "gfx/nuclear_reprocessing_site.png"){[ 0  , 0  , 64, 85, -30, -54 ]}
spriteset(gfx_nucrep_generic_building , "gfx/nuclear_reprocessing_site.png"){[ 0  , 87 , 64, 66, -31, -35 ]}


//Sprite layouts

spritelayout spritelayout_cooling_tower {
	ground { sprite : gfx_nucrep_empty_tile ; }
	building { sprite : gfx_nucrep_cooling_tower ; }
}

spritelayout spritelayout_empty_tile {
	ground { sprite : gfx_nucrep_empty_tile ; }
	//building { sprite : gfx_nucrep_empty_tile ; }
}

spritelayout spritelayout_empty_tile_cargo {
	ground { sprite : gfx_nucrep_empty_tile ; }
	building { sprite : gfx_nucrep_empty_tile_loaded ; }
}

spritelayout spritelayout_forklift {
	ground { sprite : gfx_nucrep_empty_tile ; }
	building { sprite : gfx_nucrep_forklift ; }
}

spritelayout spritelayout_forklift_cargo {
	ground { sprite : gfx_nucrep_empty_tile ; }
	building { sprite : gfx_nucrep_forklift_loaded ; }
}

spritelayout spritelayout_overpass {
	ground { sprite : gfx_nucrep_empty_tile ; }
	building { sprite : gfx_nucrep_overpass ; }
}

spritelayout spritelayout_shed {
	ground { sprite : gfx_nucrep_empty_tile ; }
	building { sprite : gfx_nucrep_shed ; }
}

spritelayout spritelayout_sphere_tank {
	ground { sprite : gfx_nucrep_empty_tile ; }
	building { sprite : gfx_nucrep_sphere_tank ; }
}

spritelayout spritelayout_road_overlay {
	ground { sprite : gfx_nucrep_road_overlay ; }
	//building { sprite : gfx_nucrep_road_overlay ; }
}

spritelayout spritelayout_chimney_building {
	ground { sprite : gfx_nucrep_empty_tile ; }
	building { sprite : gfx_nucrep_chimney_building ; }
}

spritelayout spritelayout_generic_building {
	ground { sprite : gfx_nucrep_empty_tile ; }
	building { sprite : gfx_nucrep_generic_building ; }
}

//Cargo switches

switch(FEAT_INDUSTRYTILES, PARENT, sw_nucrep_empty_cargo_decider, waiting_cargo_1 > 0){
	1 : spritelayout_empty_tile_cargo ;
	spritelayout_empty_tile ;
}

switch(FEAT_INDUSTRYTILES, PARENT, sw_nucrep_forklift_cargo_decider, waiting_cargo_1 > 0){
	1 : spritelayout_forklift_cargo ;
	spritelayout_forklift ;
}





//Tile graphics decider

switch(FEAT_INDUSTRYTILES, SELF, nucrep_tile_gfx_decider, relative_pos){
	relative_coord(0,0) : spritelayout_cooling_tower ;
	relative_coord(0,1) : spritelayout_sphere_tank ;
	relative_coord(0,2) : spritelayout_generic_building ;
	relative_coord(0,3) : spritelayout_generic_building ;
	relative_coord(0,4) : spritelayout_generic_building ;
	relative_coord(0,5) : spritelayout_sphere_tank ;
	
	relative_coord(1,0) : spritelayout_cooling_tower ;
	relative_coord(1,1) : spritelayout_shed ;
	relative_coord(1,2) : spritelayout_shed ;
	relative_coord(1,3) : spritelayout_shed ;
	relative_coord(1,4) : spritelayout_shed ;
	relative_coord(1,5) : sw_nucrep_forklift_cargo_decider ;
	//Skip a row
	relative_coord(3,0) : spritelayout_cooling_tower ;
	relative_coord(3,1) : spritelayout_chimney_building ;
	relative_coord(3,2) : sw_nucrep_forklift_cargo_decider ;
	relative_coord(3,3) : spritelayout_overpass ;
	relative_coord(3,4) : sw_nucrep_empty_cargo_decider ;
	relative_coord(3,5) : sw_nucrep_empty_cargo_decider ;
	
	relative_coord(4,0) : spritelayout_cooling_tower ;
	relative_coord(4,1) : spritelayout_chimney_building ;
	relative_coord(4,2) : sw_nucrep_empty_cargo_decider ;
	relative_coord(4,3) : spritelayout_shed ;
	relative_coord(4,4) : spritelayout_chimney_building ;
	relative_coord(4,5) : sw_nucrep_forklift_cargo_decider ;
	//Skip a row
	relative_coord(6,0) : spritelayout_road_overlay ;
	relative_coord(6,1) : spritelayout_road_overlay ;
	relative_coord(6,2) : spritelayout_road_overlay ;
	relative_coord(6,3) : spritelayout_road_overlay ;
	relative_coord(6,4) : spritelayout_road_overlay ;
	relative_coord(6,5) : spritelayout_road_overlay ;
}


//Industry tile

item(FEAT_INDUSTRYTILES, nucrep_tile){
	property{
		substitute       : 0 ;
		accepted_cargos  : [[NWST, 8]] ;
		land_shape_flags : bitmask(LSF_ONLY_ON_FLAT_LAND) ;
	}
	graphics{
		default    : nucrep_tile_gfx_decider ;
	}
}


//Tile layout

tilelayout tilelayout_nucrep_site_big{
	0,0 : nucrep_tile ;
	0,1 : nucrep_tile ;
	0,2 : nucrep_tile ;
	0,3 : nucrep_tile ;

	1,0 : nucrep_tile ;
	1,1 : nucrep_tile ;
	1,2 : nucrep_tile ;
	1,3 : nucrep_tile ;

	3,0 : nucrep_tile ;
	3,1 : nucrep_tile ;
	3,2 : nucrep_tile ;
	3,3 : nucrep_tile ;
      
	4,0 : nucrep_tile ;
	4,1 : nucrep_tile ;
	4,2 : nucrep_tile ;
	4,3 : nucrep_tile ;
	  
	6,0 : nucrep_tile ;
	6,1 : nucrep_tile ;
	6,2 : nucrep_tile ;
	6,3 : nucrep_tile ;
}

tilelayout tilelayout_nucrep_site_medium{
	0,0 : nucrep_tile ;
	0,1 : nucrep_tile ;
	0,2 : nucrep_tile ;
	0,3 : nucrep_tile ;
	0,4 : nucrep_tile ;
	0,5 : nucrep_tile ;

	1,0 : nucrep_tile ;
	1,1 : nucrep_tile ;
	1,2 : nucrep_tile ;
	1,3 : nucrep_tile ;
	1,4 : nucrep_tile ;
	1,5 : nucrep_tile ;

	3,0 : nucrep_tile ;
	3,1 : nucrep_tile ;
	3,2 : nucrep_tile ;
	3,3 : nucrep_tile ;
	3,4 : nucrep_tile ;
	3,5 : nucrep_tile ;
      
	4,0 : nucrep_tile ;
	4,1 : nucrep_tile ;
	4,2 : nucrep_tile ;
	4,3 : nucrep_tile ;
	4,4 : nucrep_tile ;
	4,5 : nucrep_tile ;
}

tilelayout tilelayout_nucrep_site_small{
	0,0 : nucrep_tile ;
	0,1 : nucrep_tile ;
	0,2 : nucrep_tile ;
	0,3 : nucrep_tile ;
	0,4 : nucrep_tile ;
	0,5 : nucrep_tile ;

	1,0 : nucrep_tile ;
	1,1 : nucrep_tile ;
	1,2 : nucrep_tile ;
	1,3 : nucrep_tile ;
	1,4 : nucrep_tile ;
	1,5 : nucrep_tile ;
}

 /* Every month, the counter is increased. The counter is reset to zero whenever the power plant
    receives cargo. When the counter reaches 61, the industry will close when the random production
    change callback is called. */
 
 /* When we receive cargo, the counter is reset. */
 produce(nucrep_cargo_arrive_produce, 0, 0, 0, 0, STORE_PERM(0, 0x00));
 
 /* Every month the counter is increased. */
 switch (FEAT_INDUSTRIES, SELF, nucrep_monthly_prod_change_switch, STORE_PERM(LOAD_PERM(0x00) + 1, 0x00)) {
 	return CB_RESULT_IND_PROD_NO_CHANGE;
 }
 
 /* Only check industry closure if the counter is greater than 60. */
 switch (FEAT_INDUSTRIES, SELF, nucrep_random_prod_change_switch, LOAD_PERM(0x00)) {
 	0..60: return CB_RESULT_IND_PROD_NO_CHANGE;
 	return CB_RESULT_IND_PROD_CLOSE;
 }

switch (FEAT_INDUSTRIES, SELF, nucrep_water_check, water_distance){
	0..32 : CB_RESULT_LOCATION_ALLOW ;
	return string(STR_ERROR_NOT_NEAR_WATER) ;
}

produce(nucrep_produce_monthly, 2, 0, 0, 0, 0);

item(FEAT_INDUSTRIES, nuclear_reprocessing_site){
	property {
		substitute            : 0x01 ;
		layouts               : [tilelayout_nucrep_site_big, tilelayout_nucrep_site_medium, tilelayout_nucrep_site_small] ;
		life_type             : IND_LIFE_TYPE_BLACK_HOLE ;
		fund_cost_multiplier  : 37 ;
		prod_cargo_types      : [] ;
		accept_cargo_types    : [cargotype("NWST")] ;
		conflicting_ind_types : [0x01] ;
		prob_random           : 3 ;
		prob_in_game          : 2 ;
		map_colour            : 255 ;
		spec_flags            : bitmask(IND_FLAG_BUILT_ONLY_AFTER_1960) ;
		new_ind_msg           : TTD_STR_NEWS_INDUSTRY_CONSTRUCTION ;
		name                  : string(STR_NUCPROC_NAME) ;
		nearby_station_name   : string(STR_NUCPROC_STATION_NAME) ;
	}
	graphics {
		produce_cargo_arrival : nucrep_cargo_arrive_produce ;
		produce_256_ticks     : nucrep_produce_monthly ;
		location_check        : nucrep_water_check ;
		monthly_prod_change   : nucrep_monthly_prod_change_switch ;
		random_prod_change    : nucrep_random_prod_change_switch ;
	}
}



/*
item(FEAT_INDUSTRYTILES, nucrep_tile_test){
	property{
		substitute       : 0 ;
		accepted_cargos  : [[NWST, 8]] ;
		land_shape_flags : bitmask(LSF_ONLY_ON_FLAT_LAND) ;
	}
	graphics{
		default    : spritelayout_generic_building ;
	}
}



tilelayout tilelayout_nucrep_site_test {
	0,0 : nucrep_tile_test ;
}

item(FEAT_INDUSTRIES, nuclear_reprocessing_site_test){
	property {
		substitute            : 0x01 ;
		layouts               : [tilelayout_nucrep_site_test] ;
		life_type             : IND_LIFE_TYPE_BLACK_HOLE ;
		fund_cost_multiplier  : 37 ;
		prod_cargo_types      : [] ;
		accept_cargo_types    : [cargotype("NWST")] ;
		conflicting_ind_types : [0x01] ;
		prob_random           : 3 ;
		prob_in_game          : 2 ;
		map_colour            : 239 ;
		spec_flags            : bitmask(IND_FLAG_BUILT_ONLY_AFTER_1960) ;
		new_ind_msg           : TTD_STR_NEWS_INDUSTRY_CONSTRUCTION ;
		name                  : string(STR_NUCPROC_NAME) ;
		nearby_station_name   : string(STR_NUCPROC_STATION_NAME) ;
	}
	graphics {
		produce_cargo_arrival : nucrep_cargo_arrive_produce ;
		produce_256_ticks     : nucrep_produce_monthly ;
		location_check        : nucrep_water_check ;
		monthly_prod_change   : nucrep_monthly_prod_change_switch ;
		random_prod_change    : nucrep_random_prod_change_switch ;
	}
}
*/