///Brush/ABB 92

//Spritesets

spriteset(gfx_abb92_railfreight       , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double(  0, 50) }
spriteset(gfx_abb92_railfreight_rev   , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double_reverse(  0, 50) }
spriteset(gfx_abb92_ews               , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double(  0, 75) }
spriteset(gfx_abb92_ews_rev           , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double_reverse(  0, 75) }
spriteset(gfx_abb92_dbcargo           , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double(  0,100) }
spriteset(gfx_abb92_dbcargo_rev       , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double_reverse(  0,100) }
spriteset(gfx_abb92_calsleeper        , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double(  0,125) }
spriteset(gfx_abb92_calsleeper_rev    , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double_reverse(  0,125) }
spriteset(gfx_abb92_gbrf              , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double(  0,150) }
spriteset(gfx_abb92_gbrf_rev          , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double_reverse(  0,150) }

spriteset(gfx_abb92_purchase_railfreight, "gfx/ELECTRIC_LOCO/class_92.png"){ template_purchase_short(178, 50) }
spriteset(gfx_abb92_purchase_ews        , "gfx/ELECTRIC_LOCO/class_92.png"){ template_purchase_short(178, 75) }
spriteset(gfx_abb92_purchase_dbcargo    , "gfx/ELECTRIC_LOCO/class_92.png"){ template_purchase_short(178,100) }
spriteset(gfx_abb92_purchase_calsleeper , "gfx/ELECTRIC_LOCO/class_92.png"){ template_purchase_short(178,125) }
spriteset(gfx_abb92_purchase_gbrf       , "gfx/ELECTRIC_LOCO/class_92.png"){ template_purchase_short(178,150) }

spriteset(gfx_abb92_pantograph          , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double(235, 0) }
spriteset(gfx_abb92_pantograph_rev      , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double(235, 25) }
spriteset(gfx_abb92_headlight           , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double(235, 50) }
spriteset(gfx_abb92_taillight           , "gfx/ELECTRIC_LOCO/class_92.png"){ template_Mk2_double(235, 75) }



//Graphics switches

switch(FEAT_TRAINS, PARENT, sw_abb92_railfreight_rev_check, vehicle_is_reversed){ 1 : gfx_abb92_railfreight_rev ; gfx_abb92_railfreight ;}
switch(FEAT_TRAINS, PARENT, sw_abb92_ews_rev_check, vehicle_is_reversed){ 1 : gfx_abb92_ews_rev ; gfx_abb92_ews ;}
switch(FEAT_TRAINS, PARENT, sw_abb92_dbcargo_rev_check, vehicle_is_reversed){ 1 : gfx_abb92_dbcargo_rev ; gfx_abb92_dbcargo ;}
switch(FEAT_TRAINS, PARENT, sw_abb92_calsleeper_rev_check, vehicle_is_reversed){ 1 : gfx_abb92_calsleeper_rev ; gfx_abb92_calsleeper ;}
switch(FEAT_TRAINS, PARENT, sw_abb92_gbrf_rev_check, vehicle_is_reversed){ 1 : gfx_abb92_gbrf_rev ; gfx_abb92_gbrf ;}


switch(FEAT_TRAINS, PARENT, sw_abb92_private_2_decider, most_common_cargo_type){
	PASS : sw_abb92_calsleeper_rev_check ;
	sw_abb92_gbrf_rev_check ;
}

switch(FEAT_TRAINS, SELF, sw_abb92_auto, build_year){
	1995..2007 : sw_abb92_ews_rev_check ;
	2008..2012 : sw_abb92_dbcargo_rev_check ;
	2013..4096 : sw_abb92_private_2_decider ;
	sw_abb92_railfreight_rev_check ;
}


switch(FEAT_TRAINS, SELF, sw_abb92_manual, cargo_subtype){
	1 : sw_abb92_ews_rev_check ;
	2 : sw_abb92_dbcargo_rev_check ;
	3 : sw_abb92_gbrf_rev_check ;
	4 : sw_abb92_calsleeper_rev_check ;
	sw_abb92_railfreight_rev_check ;
}



switch(FEAT_TRAINS, SELF, sw_abb92_manual_check, livery_choice){
	1 : sw_abb92_manual ;
	sw_abb92_auto ;
}

//Sprite stacking

switch(FEAT_TRAINS, PARENT, sw_abb92_panto_rev_check, vehicle_is_reversed){ 1 : gfx_abb92_pantograph_rev ; gfx_abb92_pantograph ; }
switch(FEAT_TRAINS, SELF, sw_abb92_panto_check, current_railtype){ ELRL : sw_abb92_panto_rev_check ; SAAZ : sw_abb92_panto_rev_check ; gfx_blank ; }

switch(FEAT_TRAINS, SELF, sw_abb92_taillight_check, position_in_consist_from_end){ 0 : gfx_abb92_taillight ; }
switch(FEAT_TRAINS, SELF, sw_abb92_headlight_check, position_in_consist){ 0 : gfx_abb92_headlight ; sw_abb92_taillight_check ; }

switch(FEAT_TRAINS, SELF, sw_abb92_spritestack, SPR_STACK){
	0 : sw_abb92_manual_check ;
	1 : sw_abb92_headlight_check ;
	//2 : sw_abb92_taillight_check ;
	2 : sw_abb92_panto_check ;
}


//Refit text

switch(FEAT_TRAINS, SELF, sw_abb92_refit_text, cargo_subtype){
	0 : string(STR_LIVERY_RAILFREIGHT, string(STR_LIVERY_EMPTY)) ;
	1 : string(STR_LIVERY_EWS) ;
	2 : string(STR_LIVERY_DBSCHENKER) ;
	3 : string(STR_LIVERY_GBRF) ;
	4 : string(STR_LIVERY_CAL_SLEEPER) ;
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_abb92_refit_text_checker, livery_choice){
	1 : sw_abb92_refit_text ;
	CB_RESULT_NO_TEXT ;
}




//Purchase

switch(FEAT_TRAINS, SELF, sw_abb92_purchase, current_year){
	1995..2007 : gfx_abb92_purchase_ews ;
	2008..2012 : gfx_abb92_purchase_dbcargo ;
	2013..4096 : gfx_abb92_purchase_gbrf ;
	gfx_abb92_purchase_railfreight ;
}

switch(FEAT_TRAINS, SELF, sw_abb92_purchase_colours, current_year){
	1995..2012 : palette_2cc(COLOUR_RED, COLOUR_YELLOW) ;
	2013..4096 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_ORANGE) ;
	palette_2cc(COLOUR_GREY, COLOUR_GREY) ;
}

switch(FEAT_TRAINS, SELF, sw_abb92_purchase_2cc_check, colour_scheme_1){
	1 : sw_abb92_purchase_colours ;
	palette_2cc(company_colour1, company_colour2) ;
}

//Colour mapping

switch(FEAT_TRAINS, SELF, sw_abb92_colours_private_2_decider, most_common_cargo_type){
	PASS : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_ORANGE) ;
}

switch(FEAT_TRAINS, SELF, sw_abb92_colours_auto, build_year){
	1995..2012 : palette_2cc(COLOUR_RED, COLOUR_YELLOW) ;
	2013..4096 : sw_abb92_colours_private_2_decider ;
	palette_2cc(COLOUR_GREY, COLOUR_GREY) ;
}


switch(FEAT_TRAINS, SELF, sw_abb92_colours_manual, cargo_subtype){
	1..2 : palette_2cc(COLOUR_RED, COLOUR_YELLOW) ;
	3    : palette_2cc(COLOUR_DARK_BLUE, COLOUR_ORANGE) ;
	4    : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	palette_2cc(COLOUR_GREY, COLOUR_GREY) ;
}


switch(FEAT_TRAINS, SELF, sw_abb92_colours_manual_check, livery_choice){
	1 : sw_abb92_colours_manual ;
	sw_abb92_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_abb92_colours_2cc_check, colour_scheme_1){
	1 : sw_abb92_colours_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}

//Other switches

switch(FEAT_TRAINS, SELF, sw_abb92_power, current_railtype){ SAA3 : return 5365 ; return 6705 ;}

switch(FEAT_TRAINS, SELF, sw_abb92_colours_dbso_check, DBSO_CHECK){ 1 : sw_dbso_colours_2cc_check ; sw_abb92_colours_2cc_check ;}
switch(FEAT_TRAINS, SELF, sw_abb92_colours_dvt_check, DVT_CHECK){ 1 : sw_dvt_colours_2cc_check ; sw_abb92_colours_dbso_check ;}

switch(FEAT_TRAINS, SELF, sw_abb92_gfx_dbso_check, DBSO_CHECK){ 1 : sw_dbso_sprite_stack ; sw_abb92_spritestack ;}
switch(FEAT_TRAINS, SELF, sw_abb92_gfx_dvt_check, DVT_CHECK){ 1 : sw_dvt_sprite_stack ; sw_abb92_gfx_dbso_check ;}

switch(FEAT_TRAINS, SELF, sw_abb92_vfx_dbso_dvt_check, (DBSO_CHECK) | (DVT_CHECK)){ 0 : vfx_dual_voltage ; }

//Item

item (FEAT_TRAINS, item_abb92, 92){
	property{
		name                           : string(STR_NAME_92) ;
		misc_flags                     : bitmask(TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC);
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		length                         : 8 ;
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date(1993,4,1) ;
		model_life                     : 30 ;
		vehicle_life                   : 45 ;
		reliability_decay              : 5 ;
		cost_factor                    : 65 ;
		running_cost_base              : RUNNING_COST_ELECTRIC ;
		running_cost_factor            : 225 ;
		refit_cost                     : 0 ;
		refittable_cargo_classes       : 0 ;
		cargo_allow_refit              : [GOOD] ;
		cargo_capacity                 : 1 ;
		track_type                     : SAAZ ;
		engine_class                   : ENGINE_CLASS_ELECTRIC ;
		speed                          : 85 mph ;
		power                          : 5000 kW ;
		weight                         : 126 ton ;
		tractive_effort_coefficient    : 0.2857142857142857 ;
		air_drag_coefficient           : 0.095 ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_ELECTRIC ;
		dual_headed                    : 0 ;
	}
	graphics{
		default                 : sw_abb92_gfx_dvt_check ;
		purchase                : sw_abb92_purchase ;
		sound_effect            : sw_sfx_electric_loco_modern ;
		create_effect           : sw_abb92_vfx_dbso_dvt_check ;
		colour_mapping          : sw_abb92_colours_dvt_check ;
		purchase_colour_mapping : sw_abb92_purchase_2cc_check ;
		cargo_subtype_text      : sw_abb92_refit_text_checker ;
		purchase_power          : 6705 ;
		power                   : sw_abb92_power ;
		purchase_cargo_capacity : 0 ;
		cargo_capacity          : return 0 ;
		can_attach_wagon        : sw_all_but_MU90;
	}
}