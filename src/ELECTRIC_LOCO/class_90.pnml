//Class 90

//Sprite sets

spriteset(gfx_cl90_intercity			, "gfx/ELECTRIC_LOCO/class_90_new.png"){ template_7_8_2		(   0,  25) }
spriteset(gfx_cl90_intercity_rev		, "gfx/ELECTRIC_LOCO/class_90_new.png"){ template_7_8_rev_2	(   0,  25) }
spriteset(gfx_cl90_railfreight			, "gfx/ELECTRIC_LOCO/class_90_new.png"){ template_7_8_2		(   0,  50) }
spriteset(gfx_cl90_railfreight_rev		, "gfx/ELECTRIC_LOCO/class_90_new.png"){ template_7_8_rev_2	(   0,  50) }

spriteset(gfx_cl90_purchase_intercity	, "gfx/ELECTRIC_LOCO/class_90_new.png"){ template_purchase	( 175,  25) }


spriteset(gfx_cl90_panto_up				, "gfx/ELECTRIC_LOCO/class_90_new.png"){ template_7_8_2		( 260,   0) }
spriteset(gfx_cl90_panto_up_rev			, "gfx/ELECTRIC_LOCO/class_90_new.png"){ template_7_8_rev_2	( 260,   0) }
spriteset(gfx_cl90_panto_down			, "gfx/ELECTRIC_LOCO/class_90_new.png"){ template_7_8_2		( 260,  25) }
spriteset(gfx_cl90_panto_down_rev		, "gfx/ELECTRIC_LOCO/class_90_new.png"){ template_7_8_rev_2	( 260,  25) }

spriteset(gfx_cl90_headlight			, "gfx/ELECTRIC_LOCO/class_90_new.png"){ template_7_8_2		( 260,  50) }
spriteset(gfx_cl90_taillight			, "gfx/ELECTRIC_LOCO/class_90_new.png"){ template_7_8_2		( 260,  75) }


//Graphics switches

switch(FEAT_TRAINS, PARENT, sw_cl90_gfx_rev_check_intercity, vehicle_is_reversed){		1 : gfx_cl90_intercity_rev ;	gfx_cl90_intercity		; }
switch(FEAT_TRAINS, PARENT, sw_cl90_gfx_rev_check_railfreight, vehicle_is_reversed){	1 : gfx_cl90_railfreight_rev ;	gfx_cl90_railfreight	; }


//Automatic

switch(FEAT_TRAINS, SELF, sw_cl90_gfx_auto_sectorised, most_common_cargo_type){
	PASS : sw_cl90_gfx_rev_check_intercity ;
	sw_cl90_gfx_rev_check_railfreight ;
}

switch(FEAT_TRAINS, SELF, sw_cl90_gfx_auto, current_year){
	sw_cl90_gfx_auto_sectorised ;
}

//Manual

switch(FEAT_TRAINS, SELF, sw_cl90_gfx_manual, cargo_subtype){
	0 : sw_cl90_gfx_rev_check_intercity ;
	sw_cl90_gfx_rev_check_railfreight ;
}

switch(FEAT_TRAINS, SELF, sw_cl90_gfx_manual_check, livery_choice){
	1 : sw_cl90_gfx_manual ;
	sw_cl90_gfx_auto ;
}


//Sprite stacking

switch(FEAT_TRAINS, PARENT, sw_cl90_gfx_panto_up_rev_check, vehicle_is_reversed){
	1 : gfx_cl90_panto_up_rev ;
	gfx_cl90_panto_up ;
}

switch(FEAT_TRAINS, PARENT, sw_cl90_gfx_panto_down_rev_check, vehicle_is_reversed){
	1 : gfx_cl90_panto_down_rev ;
	gfx_cl90_panto_down ;
}

switch(FEAT_TRAINS, SELF, sw_cl90_gfx_panto_check, vehicle_is_potentially_powered){
	1 : sw_cl90_gfx_panto_up_rev_check ;
	sw_cl90_gfx_panto_down_rev_check ;
}

switch(FEAT_TRAINS, SELF, sw_cl90_gfx_headlights_check, position_in_consist){ 0 : gfx_cl90_headlight ; }
switch(FEAT_TRAINS, SELF, sw_cl90_gfx_taillights_check, position_in_consist){ 0 : gfx_cl90_taillight ; }

switch(FEAT_TRAINS, SELF, sw_cl90_gfx_sprite_stack, SPR_STACK){
	0 : sw_cl90_gfx_manual_check ;
	1 : sw_cl90_gfx_panto_check ;
	2 : sw_cl90_gfx_headlights_check ;
	3 : sw_cl90_gfx_taillights_check ;
}

/* //Graphics DVT check

switch(FEAT_TRAINS, SELF, sw_cl90_gfx_dvt_check, DVT_CHECK){
	1 : sw_dvt_sprite_stack ;
	sw_cl90_gfx_sprite_stack ;
}
 */


//Colours

//Automatic

switch(FEAT_TRAINS, SELF, sw_cl90_colours_auto_sectorised, most_common_cargo_type){
	PASS : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	palette_2cc(COLOUR_GREY, COLOUR_GREY) ;
}

switch(FEAT_TRAINS, SELF, sw_cl90_colours_auto, current_year){
	sw_cl90_colours_auto_sectorised ;
}



//Manual

switch(FEAT_TRAINS, SELF, sw_cl90_colours_manual, cargo_subtype){
	0 : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	palette_2cc(COLOUR_GREY, COLOUR_GREY) ;
}

switch(FEAT_TRAINS, SELF, sw_cl90_colours_manual_check, livery_choice){
	1 : sw_cl90_colours_manual ;
	sw_cl90_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_cl90_colours_2cc_check, colour_scheme_1){
	1 : sw_cl90_colours_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}


/* //Colours DVT check

switch(FEAT_TRAINS, SELF, sw_cl90_colours_dvt_check, DVT_CHECK){
	1 : sw_dvt_colours_2cc_check ;
	sw_cl90_colours_2cc_check ;
} */




//Refit text

switch(FEAT_TRAINS, SELF, sw_cl90_refit_text, cargo_subtype){
	0 : string(STR_LIVERY_INTERCITY) ;
	1 : return(string(STR_LIVERY_RAILFREIGHT,string(STR_SECTOR_GENERAL))) ;
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_cl90_refit_text_check, livery_choice){
	1 : sw_cl90_refit_text ;
	CB_RESULT_NO_TEXT ;
}



//Purchase sprite

switch(FEAT_TRAINS, SELF, sw_cl90_purchase, current_year){
	gfx_cl90_purchase_intercity ;
}

switch(FEAT_TRAINS, SELF, sw_cl90_purchase_colours, current_year){
	palette_2cc(COLOUR_GREY, COLOUR_RED) ;
}

switch(FEAT_TRAINS, SELF, sw_cl90_purchase_colours_2cc_check, colour_scheme_1){
	1 : sw_cl90_purchase_colours ;
	palette_2cc(company_colour1, company_colour2) ;
}



//Item

item(FEAT_TRAINS, item_cl90, 90){
	property{
		name		                    : string(STR_NAME_90) ;
		misc_flags		                : bitmask(TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC);
		sprite_id		                : SPRITE_ID_NEW_TRAIN ;
		length		                    : 7 ;
		climates_available		        : NO_CLIMATE ;
		introduction_date		        : date(1987,1,1) ;
		model_life		                : 25 ;
		vehicle_life		            : 40 ;
		reliability_decay		        : 6 ;
		cost_factor		                : 60 ;
		running_cost_base		        : RUNNING_COST_ELECTRIC ;
		running_cost_factor		        : 255 ;
		refit_cost		                : 0 ;
		refittable_cargo_classes		: 0 ;
		cargo_allow_refit		        : [GOOD] ;
		cargo_capacity		            : 1 ;
		track_type		                : ELRL ;
		engine_class		            : ENGINE_CLASS_ELECTRIC ;
		speed		                    : 110 mph ;
		power		                    : 3700 kW ;
		weight		                    : 84.5 ton ;
		tractive_effort_coefficient		: 0.34 ;
		air_drag_coefficient		    : 0.08 ;
		effect_spawn_model_and_powered	: EFFECT_SPAWN_MODEL_ELECTRIC ;
		dual_headed						: 0 ;
	}
		graphics{
			default			                : sw_cl90_gfx_sprite_stack ;
			purchase			            : sw_cl90_purchase ;
			sound_effect			        : sw_sfx_electric_loco_old ;
			create_effect			        : sw_br87_vfx_dbso_dvt_check ;
			colour_mapping			        : sw_cl90_colours_2cc_check ;
			purchase_colour_mapping			: sw_cl90_purchase_colours_2cc_check ;
			cargo_subtype_text			    : sw_cl90_refit_text_check ;
			purchase_cargo_capacity			: 0 ;
			cargo_capacity			        : return 0 ;
			can_attach_wagon			    : sw_all_but_MU90 ;
			running_cost_factor			    : return(current_speed == 0 ? 50 : 255) ;
			purchase_running_cost_factor	: 255 ;
			purchase_power					: 3700 ;
		}
}

if (vehicle_selection < 1){
item (FEAT_TRAINS, item_cl90, 90){
	property{
		climates_available             : NO_CLIMATE ;
	}
}
}

if (alt_names == 1){
item (FEAT_TRAINS, item_cl90, 90){
	property{
		name                           : string(STR_NAME_90_ALT) ;
	}
}
}


