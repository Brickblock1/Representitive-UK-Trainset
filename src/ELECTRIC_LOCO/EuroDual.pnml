//Class 88

//Spritesets

spriteset(gfx_eurodual_drs         , "gfx/ELECTRIC_LOCO/class_88.png"){template_uklight(0,0)}
spriteset(gfx_eurodual_drs_reverse , "gfx/ELECTRIC_LOCO/class_88.png"){template_uklight_reverse(0,0)}

spriteset(gfx_eurodual_drs_purchase, "gfx/ELECTRIC_LOCO/class_88.png"){template_purchase_short(176,0)}


switch(FEAT_TRAINS, PARENT, sw_eurodual_gfx_drs_reversal_check, vehicle_is_reversed){
	1 : gfx_eurodual_drs_reverse ;
	gfx_eurodual_drs ;
}

switch(FEAT_TRAINS, SELF, sw_eurodual_spritestack, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0 : sw_eurodual_gfx_drs_reversal_check ;
	1 : sw_uklight_gfx_headlights ;
}

switch(FEAT_TRAINS, SELF, sw_eurodual_dbso_check, DBSO_CHECK){
	1 : sw_dbso_sprite_stack ;
	sw_eurodual_spritestack ;
}

switch(FEAT_TRAINS, SELF, sw_eurodual_dvt_check, DVT_CHECK){
	1 : sw_dvt_sprite_stack ;
	sw_eurodual_dbso_check ;
}


//Colour mapping

switch(FEAT_TRAINS, SELF, sw_eurodual_colours, colour_scheme_1){
	1 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	palette_2cc(company_colour1, company_colour2) ;
}

switch(FEAT_TRAINS, SELF, sw_eurodual_colours_dbso_check, DBSO_CHECK){
	1 : sw_dbso_colours_2cc_check ;
	sw_eurodual_colours ;
}

switch(FEAT_TRAINS, SELF, sw_eurodual_colours_dvt_check, DVT_CHECK){
	1 : sw_dvt_colours_2cc_check ;
	sw_eurodual_colours_dbso_check ;
}


//Effects deciders

switch(FEAT_TRAINS, SELF, sw_eurodual_sfx_decider, (STANDARD_GAUGE_CHECK_OHLE_ONLY)){
	1   : sw_sfx_electric_loco_modern ;
	//"3RDC" : sw_sfx_electric_loco_modern ;
	sw_sfx_diesel_loco_modern ;
}

switch(FEAT_TRAINS, SELF, sw_eurodual_effect_decider, (STANDARD_GAUGE_CHECK_OHLE_ONLY)){
	1   : vfx_ohle ;
	//"3RDC" : vfx_ohle ;
	vfx_diesel_default ;
}

switch(FEAT_TRAINS, SELF, sw_eurodual_effect_dbso_check, (DBSO_CHECK) | (DVT_CHECK)){
	0 : sw_eurodual_effect_decider ;
}

switch(FEAT_TRAINS, SELF, sw_eurodual_power_decider, (STANDARD_GAUGE_CHECK_OHLE_ONLY)){
	1   : 5400 ;
	//"3RDC" : 5364 ;
	1000 ;	
}

switch(FEAT_TRAINS, SELF, sw_eurodual_effect_model, (STANDARD_GAUGE_CHECK_OHLE_ONLY)){
	1   : EFFECT_SPAWN_MODEL_ELECTRIC ;
	//"3RDC" : 5364 ;
	EFFECT_SPAWN_MODEL_DIESEL ;	
}

//Item

item (FEAT_TRAINS, item_eurodual, 88){
	property{
		name                           : string(STR_NAME_EURODUAL) ;
		misc_flags                     : bitmask(TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC) ;
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		length                         : 8 ;
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date(2016, 7, 1) ;
		model_life                     : VEHICLE_NEVER_EXPIRES ;
		vehicle_life                   : 35 ;
		reliability_decay              : 9 ;
		cost_factor                    : 46 ;
		running_cost_base              : RUNNING_COST_ELECTRIC ;
		running_cost_factor            : 250 ;
		refit_cost                     : 0 ; 
		refittable_cargo_classes       : 0 ; 
		cargo_allow_refit              : [MAIL, GOOD]  ;
		cargo_capacity                 : 1 ;
		track_type                     : RAIL ;
		engine_class                   : ENGINE_CLASS_DIESEL ;
		speed                          : 100 mph ;
		power                          : 4000 kW ;
		weight                         : 86 ton ;
		tractive_effort_coefficient    : 0.3686046511627907 ;
		air_drag_coefficient           : 0.08 ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_ELECTRIC ;
		dual_headed                    : 0 ;
	}
	graphics{
		default                 : sw_eurodual_dvt_check ;
		purchase                : gfx_eurodual_drs_purchase ;
		purchase_colour_mapping : sw_eurodual_colours ;
		colour_mapping          : sw_eurodual_colours_dvt_check ;
		sound_effect            : sw_eurodual_sfx_decider ;
		create_effect           : sw_eurodual_effect_dbso_check ;
		purchase_power          : 5400 ;
		purchase_cargo_capacity : 0 ;
		cargo_capacity          : return 0 ;
		power                   : sw_eurodual_power_decider;
		can_attach_wagon        : sw_all_but_MU90;
		running_cost_factor     : return(current_speed == 0 ? 60 : 250) ;
		purchase_running_cost_factor : 250 ;
		additional_text			: string(STR_ADD_INFO_88) ;
		effect_spawn_model_and_powered : sw_eurodual_effect_model ;
	}
}

if (vehicle_selection < 1){
item (FEAT_TRAINS, item_eurodual, 88){
	property{
		climates_available             : NO_CLIMATE ;
	}
}
}


if (alt_names == 1){
item (FEAT_TRAINS, item_eurodual, 88){
	property{
		name                           : string(STR_NAME_EURODUAL_ALT) ;
	}
}
}