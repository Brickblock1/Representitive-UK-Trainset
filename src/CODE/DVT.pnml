//DVT Source

#define DVT_CHECK vehicle_is_reversed && (count_veh_id(9035) > 0)
#define DVT_REVERSE_CHECK vehicle_is_reversed && (vehicle_type_id == 37 | vehicle_type_id == 68 | vehicle_type_id == 86 | vehicle_type_id == 87 | vehicle_type_id == 88 | vehicle_type_id == 92 )

// | vehicle_type_id == 92)


/*

Liveries needed

Intercity
Virgin Trains
Greater Anglia  - !!
Arriva Trains Wales
Network Rail
Chiltern Railways  - !!

Mark 4

GNER
East Coast
Virgin East Coast

*/


//Spritesets

spriteset(gfx_dvt_generic       , "gfx/WAGON/dvt.png"){ template_Mk2_double(  0,  0)         }
spriteset(gfx_dvt_generic_rev   , "gfx/WAGON/dvt.png"){ template_Mk2_double_reverse(  0,  0) }
spriteset(gfx_dvt_intercity     , "gfx/WAGON/dvt.png"){ template_Mk2_double(  0, 25)         }
spriteset(gfx_dvt_intercity_rev , "gfx/WAGON/dvt.png"){ template_Mk2_double_reverse(  0, 25) }
spriteset(gfx_dvt_virgin        , "gfx/WAGON/dvt.png"){ template_Mk2_double(  0, 50)         }
spriteset(gfx_dvt_virgin_rev    , "gfx/WAGON/dvt.png"){ template_Mk2_double_reverse(  0, 50) }
spriteset(gfx_dvt_atw_exec      , "gfx/WAGON/dvt.png"){ template_Mk2_double(  0, 75)         }
spriteset(gfx_dvt_atw_exec_rev  , "gfx/WAGON/dvt.png"){ template_Mk2_double_reverse(  0, 75) }
spriteset(gfx_dvt_chiltern      , "gfx/WAGON/dvt.png"){ template_Mk2_double(  0,100)         }
spriteset(gfx_dvt_chiltern_rev  , "gfx/WAGON/dvt.png"){ template_Mk2_double_reverse(  0,100) }

spriteset(gfx_dvt_taillights    , "gfx/WAGON/dvt.png"){ template_Mk2_double(259,  0)         }
spriteset(gfx_dvt_headlights    , "gfx/WAGON/dvt.png"){ template_Mk2_double_reverse(259, 25) }




//Switches


//Graphics switches

switch(FEAT_TRAINS, PARENT, sw_dvt_generic_rev_check, DVT_REVERSE_CHECK){ 1 : gfx_dvt_generic_rev ; gfx_dvt_generic ; }
switch(FEAT_TRAINS, PARENT, sw_dvt_intercity_rev_check, DVT_REVERSE_CHECK){ 1 : gfx_dvt_intercity_rev ; gfx_dvt_intercity ; }
switch(FEAT_TRAINS, PARENT, sw_dvt_virgin_rev_check, DVT_REVERSE_CHECK){ 1 : gfx_dvt_virgin_rev ; gfx_dvt_virgin ; }
switch(FEAT_TRAINS, PARENT, sw_dvt_atw_exec_rev_check, DVT_REVERSE_CHECK){ 1 : gfx_dvt_atw_exec_rev ; gfx_dvt_atw_exec ; }
switch(FEAT_TRAINS, PARENT, sw_dvt_chiltern_rev_check, DVT_REVERSE_CHECK){ 1 : gfx_dvt_chiltern_rev ; gfx_dvt_chiltern ; }



//Automatix GFX

switch(FEAT_TRAINS, SELF, sw_dvt_auto, current_year){
	ERA_SECTORISATION : sw_dvt_intercity_rev_check ;
	2014..4096 : sw_dvt_chiltern_rev_check ;
	sw_dvt_virgin_rev_check ;
}


//Manual GFX

switch(FEAT_TRAINS, PARENT, sw_dvt_manual_br87, cargo_subtype){
	1 : sw_dvt_intercity_rev_check ;
	sw_dvt_virgin_rev_check ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_manual_al6, cargo_subtype){
	2 : sw_dvt_intercity_rev_check ;
	sw_dvt_virgin_rev_check ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_manual_cl90, cargo_subtype){
	0..1 : sw_dvt_intercity_rev_check ;
	sw_dvt_virgin_rev_check ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_manual_uklight, cargo_subtype){
	1 : sw_dvt_atw_exec_rev_check ;
	3 : sw_dvt_chiltern_rev_check ;
	sw_dvt_virgin_rev_check ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_manual_ee3, cargo_subtype){
	10     : sw_dvt_atw_exec_rev_check ;
	11..12 : sw_dvt_generic_rev_check ;
	sw_dvt_virgin_rev_check ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_manual_abb92, cargo_subtype){
	0    : sw_dvt_intercity_rev_check ;
	1..2 : sw_dvt_atw_exec_rev_check ;
	3    : sw_dvt_generic_rev_check ;
	sw_dvt_virgin_rev_check ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_manual, vehicle_type_id){
	37 : sw_dvt_manual_ee3 ;
	68 : sw_dvt_manual_uklight ;
	86 : sw_dvt_manual_al6 ;
	87 : sw_dvt_manual_br87 ;
	90 : sw_dvt_manual_cl90 ;
	92 : sw_dvt_manual_abb92 ;
	sw_dvt_virgin_rev_check ;
}



switch(FEAT_TRAINS, SELF, sw_dvt_manual_check, livery_choice){
	1 : sw_dvt_manual ;
	sw_dvt_auto ;
}

//Sprite stacking & lights deciders

switch(FEAT_TRAINS, SELF, sw_dvt_taillight_check, position_in_consist_from_end){ 0 : gfx_dvt_taillights ; }

switch(FEAT_TRAINS, SELF, sw_dvt_headlight_check, position_in_consist){
	0 : gfx_dvt_headlights ;
	sw_dvt_taillight_check ;
}


switch(FEAT_TRAINS, SELF, sw_dvt_sprite_stack, SPR_STACK){
	0 : sw_dvt_manual_check ;
	1 : sw_dvt_headlight_check ;
}



//Colour mapping

switch(FEAT_TRAINS, PARENT, sw_dvt_colours_auto, current_year){
	ERA_SECTORISATION : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	2014..4096 : palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}


switch(FEAT_TRAINS, PARENT, sw_dvt_colours_br87, cargo_subtype){
	1 : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_colours_cl90, cargo_subtype){
	0..1 : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_colours_al6, cargo_subtype){
	2 : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_colours_uklight, cargo_subtype){
	1 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	3 : palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_colours_ee3, cargo_subtype){
	10     : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	11..12 : palette_2cc(COLOUR_YELLOW, COLOUR_YELLOW) ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_colours_abb92, cargo_subtype){
	0    : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	1..2 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	3    : palette_2cc(COLOUR_YELLOW, COLOUR_YELLOW) ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, PARENT, sw_dvt_colours_manual, vehicle_type_id){
	37 : sw_dvt_colours_ee3 ;
	68 : sw_dvt_colours_uklight ;
	86 : sw_dvt_colours_al6 ;
	87 : sw_dvt_colours_br87 ;
	90 : sw_dvt_colours_cl90 ;
	92 : sw_dvt_colours_abb92 ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}


switch(FEAT_TRAINS, SELF, sw_dvt_colours_manual_check, livery_choice){
	1 : sw_dvt_colours_manual ;
	sw_dvt_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_dvt_colours_2cc, current_year){
	2014..4096 : palette_2cc(company_colour2, company_colour1) ; //Inverted company colours so that it works with the rest of the carriages
	palette_2cc(company_colour1, company_colour2) ;
}

switch(FEAT_TRAINS, SELF, sw_dvt_colours_2cc_check, colour_scheme_1){
	1 : sw_dvt_colours_manual_check ;
	sw_dvt_colours_2cc ;
}