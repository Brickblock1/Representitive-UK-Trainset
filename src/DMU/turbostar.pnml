//TURBOSTAR


//00 - Chiltern Railways - a
//01 - Central Trains
//02 - Cross Country - a
//03 - TransPennine Express
//04 - First Scot RAIL
//05 - ScotRail Saltaire
//06 - Southern
//dd  - London Midland (170) - a
//08 - West Midlands Railway (170) - a
//09 - London Midland (172) - a
//10 - West Midlands Railway (172) - a

template template_turbo_single(x,y){
	[x+0,y,8,24,-3,-11] //North
	[x+10,y,20,16,-13,-8] //North East
	[x+32,y,32,11,-16,-7] //East
	[x+66,y,20,16,-5,-8] //South East
}

template template_turbo_double(x,y){
	[x+0,y,8,24,-3,-11] //North
	[x+10,y,20,16,-13,-8] //North East
	[x+32,y,32,11,-16,-7] //East
	[x+66,y,20,16,-5,-8] //South East
	[x+88,y,8,24,-3,-11] //South
	[x+98,y,20,16,-13,-8] //South West
	[x+120,y,32,11,-16,-7] //West
	[x+154,y,20,16,-5,-8] //North West
}

//Spritesets

spriteset (cl168_cm_front, "gfx/DMU/class_168.png"){template_turbo_double(0,0) }
spriteset (cl168_cm_back,  "gfx/DMU/class_168.png"){template_turbo_double(0,26)}
spriteset (cl168_cm_mid,   "gfx/DMU/class_168.png"){template_turbo_single(0,52)}
//spriteset (cl172_lm_front, "gfx/DMU/class_172.png"){template_turbo_double(0,0) }
//spriteset (cl172_lm_back,  "gfx/DMU/class_172.png"){template_turbo_double(0,26)}
//spriteset (cl172_lm_mid,   "gfx/DMU/class_172.png"){template_turbo_single(0,52)}

spriteset (cl168_purchase, "gfx/DMU/class_168.png"){template_dualhead_purchase(88,52)}
//spriteset (cl172_purchase, "gfx/DMU/class_172.png"){template_dualhead_purchase(88,52)}

spriteset (turbostar_lm_170				, "gfx/DMU/turbostar.png"){template_8_8_networker		( 179, 050)}
spriteset (turbostar_lm_170_mid			, "gfx/DMU/turbostar.png"){template_8_4_networker		( 179, 075)}
spriteset (turbostar_lm_172				, "gfx/DMU/turbostar.png"){template_8_8_networker		( 000, 000)}
spriteset (turbostar_lm_172_mid			, "gfx/DMU/turbostar.png"){template_8_4_networker		( 000, 025)}
spriteset (turbostar_wmr_172			, "gfx/DMU/turbostar.png"){template_8_8_networker		( 000, 050)}
spriteset (turbostar_wmr_172_mid		, "gfx/DMU/turbostar.png"){template_8_4_networker		( 000, 075)}

spriteset (turbostar_headlight			, "gfx/DMU/turbostar.png"){template_8_8_networker		( 000, 100)}
spriteset (turbostar_taillight			, "gfx/DMU/turbostar.png"){template_8_8_networker		( 000, 125)}

spriteset (turbostar_purchase_lm_170	, "gfx/DMU/turbostar.png"){template_dualhead_purchase	( 269, 075)}
spriteset (turbostar_purchase_lm_172	, "gfx/DMU/turbostar.png"){template_dualhead_purchase	( 090, 025)}
spriteset (turbostar_purchase_wmr_172	, "gfx/DMU/turbostar.png"){template_dualhead_purchase	( 090, 075)}


//Graphics

switch(FEAT_TRAINS, SELF, sw_168_old_2, position_in_consist_from_end){
	0 : cl168_cm_back ;
	cl168_cm_front ;
}

switch(FEAT_TRAINS, SELF, sw_168_old_1, vehicle_type_id){ 9090 : cl168_cm_mid ; sw_168_old_2 ;}

switch(FEAT_TRAINS, SELF, sw_170_lm_id_check	, vehicle_type_id ){ 9090 : turbostar_lm_170_mid ; turbostar_lm_170 ;}
switch(FEAT_TRAINS, SELF, sw_172_lm_id_check	, vehicle_type_id ){ 9090 : turbostar_lm_172_mid ; turbostar_lm_172 ;}
switch(FEAT_TRAINS, SELF, sw_172_wmr_id_check	, vehicle_type_id ){ 9090 : turbostar_wmr_172_mid ; turbostar_wmr_172 ;}



switch(FEAT_TRAINS, SELF, sw_168_auto, current_year){
	0000..2006 : sw_168_old_1 ;
	2018..4096 : sw_170_lm_id_check ;
	sw_170_lm_id_check ;
}

switch(FEAT_TRAINS, SELF, sw_172_auto, current_year){
	2018..4096 : sw_172_wmr_id_check ;
	sw_172_lm_id_check ;
}

switch(FEAT_TRAINS, SELF, sw_168_gfx_auto_year_check, build_year){
	0000..2009 : sw_168_auto ;
	sw_172_auto ;
}

switch(FEAT_TRAINS, SELF, sw_168_gfx_manual, cargo_subtype){
	0	: sw_168_old_1 ;
	9	: sw_172_lm_id_check ;
	10	: sw_172_wmr_id_check ;
	sw_170_lm_id_check ;
}

switch(FEAT_TRAINS, SELF, sw_168_gfx_manual_check, livery_choice){
	1 : sw_168_gfx_manual ;
	sw_168_gfx_auto_year_check ;
}


//Sprite stack

switch(FEAT_TRAINS, SELF, sw_168_headlights, position_in_consist){ 0 : turbostar_headlight ;}
switch(FEAT_TRAINS, SELF, sw_168_taillights, position_in_consist_from_end){ 0 : turbostar_taillight ;}

switch(FEAT_TRAINS, SELF, sw_168_sprite_stack, SPR_STACK){
	0 : sw_168_gfx_manual_check ;
	1 : sw_168_headlights ;
	2 : sw_168_taillights ;
}


//Purchase

switch(FEAT_TRAINS, SELF, sw_168_purchase, current_year){
	0000..2006 : cl168_purchase ;
	2007..2009 : turbostar_purchase_lm_170 ;
	2010..2017 : turbostar_purchase_lm_172 ;
	turbostar_purchase_wmr_172 ;
}

switch(FEAT_TRAINS, SELF, sw_168_purchase_colours, current_year){
	1996..2006 : palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	2007..2017 : palette_2cc(COLOUR_DARK_GREEN, COLOUR_GREEN) ;
	palette_2cc(COLOUR_ORANGE, COLOUR_MAUVE) ;
}

switch(FEAT_TRAINS, SELF, sw_168_purchase_2cc_check, colour_scheme_1){
	1 : sw_168_purchase_colours ;
	palette_2cc(company_colour1, company_colour2) ;
}


//Refit text

switch(FEAT_TRAINS, SELF, sw_168_refit_text, cargo_subtype){
	0	: string(STR_LIVERY_CHILTERN) ;
	1	: string(STR_LIVERY_CENTRAL) ;
	2	: string(STR_LIVERY_ARRIVA_XC) ;
	3	: string(STR_LIVERY_FIRST_TPE) ;
	4	: string(STR_LIVERY_FIRST_SCOT_RAIL) ;
	5	: string(STR_LIVERY_SCOTRAIL) ;
	6	: string(STR_LIVERY_SOUTHERN) ;
	7	: string(STR_LIVERY_LONDON_MIDLAND_STRING, string(STR_170)) ;
	8	: string(STR_LIVERY_WEST_MIDLANDS_RAILWAY_STRING, string(STR_170)) ;
	9	: string(STR_LIVERY_LONDON_MIDLAND_STRING, string(STR_172)) ;
	10	: string(STR_LIVERY_WEST_MIDLANDS_RAILWAY_STRING, string(STR_172)) ;
	CB_RESULT_NO_TEXT ;
}

//00 - Chiltern Railways - a
//01 - Central Trains
//02 - Cross Country - a
//03 - TransPennine Express
//04 - First Scot RAIL
//05 - ScotRail Saltaire
//06 - Southern
//dd  - London Midland (170) - a
//08 - West Midlands Railway (170) - a
//09 - London Midland (172) - a
//10 - West Midlands Railway (172) - a

switch(FEAT_TRAINS, SELF, sw_168_refit_text_check, livery_choice){
	1 : sw_168_refit_text ;
	CB_RESULT_NO_TEXT ;
}

//Colour mapping

switch(FEAT_TRAINS, SELF, sw_168_colours_auto, current_year){
	1996..2006 : palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	2018..4096 : palette_2cc(COLOUR_ORANGE, COLOUR_MAUVE) ;
	palette_2cc(COLOUR_DARK_GREEN, COLOUR_GREEN) ;
}

switch(FEAT_TRAINS, SELF, sw_168_colours_manual, cargo_subtype){
	0	: palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	1	: palette_2cc(COLOUR_GREEN, COLOUR_DARK_BLUE) ;
	2	: palette_2cc(COLOUR_PINK, COLOUR_RED) ;
	3	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_PINK) ;
	4	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	5	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	6	: palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN) ;
	8	: palette_2cc(COLOUR_ORANGE, COLOUR_MAUVE) ;
	10	: palette_2cc(COLOUR_ORANGE, COLOUR_MAUVE) ;
	palette_2cc(COLOUR_DARK_GREEN, COLOUR_GREEN) ;
}

switch(FEAT_TRAINS, SELF, sw_168_colours_manual_check, livery_choice){
	1 : sw_168_colours_manual ;
	sw_168_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_168_colours_2cc_check, colour_scheme_1){
	1 : sw_168_colours_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}

switch(FEAT_TRAINS, SELF, sw_168_coupling_restrictions, vehicle_type_id) {
    item_MU90: return CB_RESULT_ATTACH_ALLOW;
	item_cl168: return CB_RESULT_ATTACH_ALLOW;
    return string(STR_168_NO_COUPLE);
}


//Item 


item (FEAT_TRAINS, item_cl168, 168){
	property{
		name                           : string(STR_NAME_TURBOSTAR) ;
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date(1997, 1, 1);
		refit_cost                     : 0 ;
		refittable_cargo_classes       : CC_PASSENGERS ;
		cargo_allow_refit              : [PASS, TOUR] ;
		cargo_capacity                 : 80 ;
		model_life                     : 18 ;
		vehicle_life                   : 25 ;
		reliability_decay              : 10 ;
		cost_factor                    : 56 ;
		running_cost_base              : RUNNING_COST_DIESEL ;
		running_cost_factor            : 56 ;
		speed                          : 100 mph ;
		track_type                     : RAIL ;
		power                          : 700 kW ;
		weight                         : 45 ton ;
		engine_class                   : ENGINE_CLASS_DIESEL ;
		tractive_effort_coefficient    : 0.1 ;
		air_drag_coefficient           : 0.08 ;
		length                         : 8 ;
		misc_flags                     : bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK) ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_DIESEL ;
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		dual_headed                    : 1 ;
		loading_speed                  : 23 ;
	}
	graphics{
		can_attach_wagon:		sw_168_coupling_restrictions;
		default                 : sw_168_sprite_stack ;
		colour_mapping          : sw_168_colours_2cc_check ;
		purchase                : sw_168_purchase ;
		purchase_colour_mapping : sw_168_purchase_2cc_check ;
		sound_effect            : sw_sfx_dmu_one ;
		create_effect           : vfx_diesel_default ;
		cargo_subtype_text      : sw_168_refit_text_check ;
		running_cost_factor     : return(current_speed == 0 ? 12 : 46) ;
		purchase_running_cost_factor : 46 ;
	}
	livery_override (item_MU90){
		default                     : sw_168_gfx_manual_check ;
		colour_mapping              : sw_168_colours_2cc_check ;
		power                       : 450;
		cargo_capacity              : 80 ;
		cargo_subtype_text          : sw_168_refit_text_check ;
		weight                      : 45 ;
	}
}

if (vehicle_selection < 2){
item (FEAT_TRAINS, item_cl168, 168){
	property{
		climates_available             : NO_CLIMATE ;
	}
}
}


if (alt_names == 1){
item (FEAT_TRAINS, item_cl168, 168){
	property{
		name                           : string(STR_NAME_TURBOSTAR_ALT) ;
	}
}
}