//Express Sprinter

/*

Liveries

Regional Railways --
Trans Pennine Express --
WYPTE Metro --
Northern Rail --
New Northern --

Alphaline --
!Arriva Trains Wales
!Arrive Trains Wales (Executive)
!First Great Western
Great Western Railway --
!East Midlands Trains

Network South East --
!South West Trains
!South Western Railway

*/



//Sprite sets

spriteset(gfx_expsprint_base            , "gfx/DMU/class_158.png") { template_Mk2_double         (  0,  0) }
spriteset(gfx_expsprint_base_rev        , "gfx/DMU/class_158.png") { template_Mk2_double_reverse (  0,  0) }
spriteset(gfx_expsprint_base_mid        , "gfx/DMU/class_158.png") { template_Mk2_single         (259,  0) }
spriteset(gfx_expsprint_rr            	, "gfx/DMU/class_158.png") { template_Mk2_double         (  0,175) }
spriteset(gfx_expsprint_rr_mid        	, "gfx/DMU/class_158.png") { template_Mk2_single 		 (259,175) }
spriteset(gfx_expsprint_fgw             , "gfx/DMU/class_158.png") { template_Mk2_double         (  0, 25) }
spriteset(gfx_expsprint_fgw_mid         , "gfx/DMU/class_158.png") { template_Mk2_single         (259, 25) }
spriteset(gfx_expsprint_swt             , "gfx/DMU/class_158.png") { template_Mk2_double         (  0, 50) }
spriteset(gfx_expsprint_swt_mid         , "gfx/DMU/class_158.png") { template_Mk2_single         (259, 50) }
spriteset(gfx_expsprint_atw_refurb      , "gfx/DMU/class_158.png") { template_Mk2_double         (  0, 75) }
spriteset(gfx_expsprint_atw_refurb_mid  , "gfx/DMU/class_158.png") { template_Mk2_single         (259, 75) }
spriteset(gfx_expsprint_atw             , "gfx/DMU/class_158.png") { template_Mk2_double         (  0,100) }
spriteset(gfx_expsprint_atw_mid         , "gfx/DMU/class_158.png") { template_Mk2_single         (259,100) }
spriteset(gfx_expsprint_atw_exec        , "gfx/DMU/class_158.png") { template_Mk2_double         (  0,125) }
spriteset(gfx_expsprint_atw_exec_mid    , "gfx/DMU/class_158.png") { template_Mk2_single         (259,125) }
spriteset(gfx_expsprint_swr        		, "gfx/DMU/class_158.png") { template_Mk2_double         (  0,150) }
spriteset(gfx_expsprint_swr_mid    		, "gfx/DMU/class_158.png") { template_Mk2_single         (259,150) }
spriteset(gfx_expsprint_fsr       		, "gfx/DMU/class_158.png") { template_Mk2_double         (  0,200) }
spriteset(gfx_expsprint_fsr_mid    		, "gfx/DMU/class_158.png") { template_Mk2_single         (259,200) }
spriteset(gfx_expsprint_gwr       		, "gfx/DMU/class_158.png") { template_Mk2_double         (  0,225) }
spriteset(gfx_expsprint_gwr_mid    		, "gfx/DMU/class_158.png") { template_Mk2_single         (259,225) }

spriteset(gfx_expsprint_purchase_rr    	, "gfx/DMU/class_158.png") { template_dualhead_purchase (178,175) }
spriteset(gfx_expsprint_purchase_fsr    , "gfx/DMU/class_158.png") { template_dualhead_purchase (178,200) }
spriteset(gfx_expsprint_purchase_atw_r  , "gfx/DMU/class_158.png") { template_dualhead_purchase (178, 75) }
spriteset(gfx_expsprint_purchase_swr    , "gfx/DMU/class_158.png") { template_dualhead_purchase (178,150) }
spriteset(gfx_expsprint_purchase_fgw    , "gfx/DMU/class_158.png") { template_dualhead_purchase (178, 25) }


//Graphics switches

switch(FEAT_TRAINS, SELF, sw_expsprint_base_id_check,        vehicle_type_id){ 9030 : gfx_expsprint_base_mid ; gfx_expsprint_base ; }
switch(FEAT_TRAINS, SELF, sw_expsprint_rr_id_check,        vehicle_type_id){ 9030 : gfx_expsprint_rr_mid ; gfx_expsprint_rr ; }
switch(FEAT_TRAINS, SELF, sw_expsprint_fgw_id_check ,        vehicle_type_id){ 9030 : gfx_expsprint_fgw_mid ; gfx_expsprint_fgw ; }
switch(FEAT_TRAINS, SELF, sw_expsprint_swt_id_check ,        vehicle_type_id){ 9030 : gfx_expsprint_swt_mid ; gfx_expsprint_swt ; }
switch(FEAT_TRAINS, SELF, sw_expsprint_atw_id_check ,        vehicle_type_id){ 9030 : gfx_expsprint_atw_mid ; gfx_expsprint_atw ; }
switch(FEAT_TRAINS, SELF, sw_expsprint_atw_refurb_id_check , vehicle_type_id){ 9030 : gfx_expsprint_atw_refurb_mid ; gfx_expsprint_atw_refurb ; }
switch(FEAT_TRAINS, SELF, sw_expsprint_atw_exec_id_check   , vehicle_type_id){ 9030 : gfx_expsprint_atw_exec_mid ; gfx_expsprint_atw_exec ; }
switch(FEAT_TRAINS, SELF, sw_expsprint_swr_id_check   , vehicle_type_id){ 9030 : gfx_expsprint_swr_mid ; gfx_expsprint_swr ; }
switch(FEAT_TRAINS, SELF, sw_expsprint_fsr_id_check   , vehicle_type_id){ 9030 : gfx_expsprint_fsr_mid ; gfx_expsprint_fsr ; }
switch(FEAT_TRAINS, SELF, sw_expsprint_gwr_id_check   , vehicle_type_id){ 9030 : gfx_expsprint_gwr_mid ; gfx_expsprint_gwr ; }



switch(FEAT_TRAINS, SELF, sw_expsprint_gfx_auto, current_year){
	1987..1995 : sw_expsprint_rr_id_check ;
	1996..2003 : sw_expsprint_fsr_id_check ;
	2004..2011 : sw_expsprint_fsr_id_check ;
	2012..2017 : sw_expsprint_atw_refurb_id_check ;
	2018..4096 : sw_expsprint_swr_id_check ;
	sw_expsprint_fgw_id_check ;
}

switch(FEAT_TRAINS, SELF, sw_expsprint_gfx_manual, cargo_subtype){
	1 : sw_expsprint_atw_id_check ;
	2 : sw_expsprint_atw_refurb_id_check ;
	3 : sw_expsprint_atw_exec_id_check ;
	4 : sw_expsprint_fgw_id_check ;
	5 : sw_expsprint_gwr_id_check ;
	6 : sw_expsprint_swt_id_check ;
	7 : sw_expsprint_swr_id_check ;
	8 : sw_expsprint_fsr_id_check ;
	sw_expsprint_rr_id_check ;
}


switch(FEAT_TRAINS, SELF, sw_expsprint_gfx_manual_check, livery_choice){
	1 : sw_expsprint_gfx_manual ;
	sw_expsprint_gfx_auto ;
}


//Colours

switch(FEAT_TRAINS, SELF, sw_expsprint_colours_auto, current_year){
	1987..1995 : palette_2cc(COLOUR_DARK_BLUE, company_colour2) ;
	1996..2003 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	2004..2011 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	2012..2017 : palette_2cc(COLOUR_BLUE, COLOUR_YELLOW) ;
	2018..4096 : palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_PINK) ;
}

switch(FEAT_TRAINS, SELF, sw_expsprint_colours_manual, cargo_subtype){
	1 : palette_2cc(COLOUR_BLUE, COLOUR_YELLOW) ;
	2 : palette_2cc(COLOUR_BLUE, COLOUR_YELLOW) ;
	3 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	4 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PINK) ;
	5 : palette_2cc(COLOUR_DARK_GREEN, COLOUR_WHITE) ;
	6 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ;
	7 : palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	8 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	palette_2cc(COLOUR_DARK_BLUE, company_colour2) ;
}


switch(FEAT_TRAINS, SELF, sw_expsprint_colours_manual_check, livery_choice){
	1 : sw_expsprint_colours_manual ;
	sw_expsprint_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_expsprint_colours_2cc_check, colour_scheme_1){
	1 : sw_expsprint_colours_manual_check ;
	palette_2cc(company_colour1, company_colour1) ;
}


//Purchase sprite

switch(FEAT_TRAINS, SELF, sw_expsprint_purchase_sprite, current_year){
	1987..1995 : gfx_expsprint_purchase_rr ;
	1996..2003 : gfx_expsprint_purchase_fsr ;
	2004..2011 : gfx_expsprint_purchase_fsr ;
	2012..2017 : gfx_expsprint_purchase_atw_r ;
	2018..4096 : gfx_expsprint_purchase_swr ;
	gfx_expsprint_purchase_fgw ;
}

switch(FEAT_TRAINS, SELF, sw_expsprint_purchase_sprite_colours, current_year){
	1987..1995 : palette_2cc(COLOUR_DARK_BLUE, company_colour2) ;
	1996..2003 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	2004..2011 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	2012..2017 : palette_2cc(COLOUR_BLUE, COLOUR_YELLOW) ;
	2018..4096 : palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_PINK) ;
}

switch(FEAT_TRAINS, SELF, sw_expsprint_purchase_sprite_colours_2cc_check, colour_scheme_1){
	1 : sw_expsprint_purchase_sprite_colours ;
	palette_2cc(company_colour1, company_colour1) ;
}




//Refit text

switch(FEAT_TRAINS, SELF, sw_expsprint_refit_text, cargo_subtype){
	0 : string(STR_LIVERY_REG_RAILWAYS) ;
	1 : string(STR_LIVERY_ATW) ;
	2 : string(STR_LIVERY_ATW_REFURB) ;
	3 : string(STR_LIVERY_ATW_EXEC) ;
	4 : string(STR_LIVERY_FIRST_FGW, string(STR_LIVERY_EMPTY)) ;
	5 : string(STR_LIVERY_FIRST_GWR, string(STR_LIVERY_EMPTY)) ;
	6 : string(STR_LIVERY_SWT) ;
	7 : string(STR_LIVERY_SWR) ;
	8 : string(STR_LIVERY_FIRST_SCOT_RAIL, string(STR_LIVERY_EMPTY)) ;
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_expsprint_refit_text_check, livery_choice){
	1 : sw_expsprint_refit_text ;
	CB_RESULT_NO_TEXT ;
}




//Item

switch(FEAT_TRAINS, SELF, sw_expsprint_attach, vehicle_type_id){
	37   : return CB_RESULT_ATTACH_ALLOW  ;
	150  : return CB_RESULT_ATTACH_ALLOW  ;
	153  : return CB_RESULT_ATTACH_ALLOW  ;
	155  : return CB_RESULT_ATTACH_ALLOW  ;
	158  : return CB_RESULT_ATTACH_ALLOW  ;
	9030 : return CB_RESULT_ATTACH_ALLOW  ;
	return string(STR_158_NO_COUPLE) ;
}


item(FEAT_TRAINS, item_expsprint, 158){
	property{
		name                           : string(STR_NAME_EXPRESS_SPRINTER) ;
		misc_flags                     : bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		length                         : 8 ;
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date(1988,1,1) ;
		model_life                     : 25 ;
		vehicle_life                   : 40 ;
		reliability_decay              : 7 ;
		cost_factor                    : 65 ;
		running_cost_base              : RUNNING_COST_DIESEL ;
		running_cost_factor            : 44 ;
		refit_cost                     : 0 ;
		refittable_cargo_classes       : CC_PASSENGERS ;
		cargo_allow_refit              : [PASS, TOUR] ;
		cargo_capacity                 : 75 ;
		loading_speed                  : 19 ;
		track_type                     : RAIL ;
		engine_class                   : ENGINE_CLASS_DIESEL ;
		speed                          : 90 mph ;
		power                          : 522 kW ;
		weight                         : 38.5 ton ;
		tractive_effort_coefficient    : 0.175 ;
		air_drag_coefficient           : 0.09 ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_DIESEL ;
		dual_headed                    : 1 ;
	}
	graphics{
		default                 : sw_expsprint_gfx_manual_check ;
		purchase                : sw_expsprint_purchase_sprite ;
		sound_effect            : sw_sfx_dmu_one ;
		create_effect           : vfx_diesel_default ;
		colour_mapping          : sw_expsprint_colours_2cc_check ;
		purchase_colour_mapping : sw_expsprint_purchase_sprite_colours_2cc_check ;
		cargo_subtype_text      : sw_expsprint_refit_text_check ;
		can_attach_wagon        : sw_expsprint_attach ;
		running_cost_factor     : return(current_speed == 0 ? 11 : 44) ;
		purchase_running_cost_factor : 44 ;
	}
	livery_override (9030){
		default                     : sw_expsprint_gfx_manual_check ;
		colour_mapping              : sw_expsprint_colours_2cc_check ;
		cargo_capacity              : 75 ;
		power                       : 261 ;
		cargo_subtype_text          : sw_expsprint_refit_text_check ;
		weight                      : 38 ;
	}
}


if (alt_names == 1){
item (FEAT_TRAINS, item_expsprint, 158){
	property{
		name                           : string(STR_NAME_EXPRESS_SPRINTER_ALT) ;
	}
}
}