//Northern class 195
spriteset(spriteset_195_f_nor, "gfx/UNUSED & MIXED/civity.png") {template_8_8_networker(0, 0)}
spriteset(spriteset_195_t_nor, "gfx/UNUSED & MIXED/civity.png") {template_8_4_networker(0, 50)}
//Purchase
spriteset(spriteset_195_purchase, "gfx/UNUSED & MIXED/civity.png") {template_dualhead_purchase(0, 238)}
//Headlights
spriteset(spriteset_195_hl, "gfx/UNUSED & MIXED/civity.png") {template_8_8_networker(0, 175)}
spriteset(spriteset_195_rl, "gfx/UNUSED & MIXED/civity.png") {template_8_8_networker(0, 200)}

switch(FEAT_TRAINS, SELF, sw_195_f_auto, current_year){
spriteset_195_f_nor;
}
switch(FEAT_TRAINS, SELF, sw_195_t_auto, current_year){
spriteset_195_t_nor;
}

switch(FEAT_TRAINS, SELF, sw_195_f_man, cargo_subtype){
0: spriteset_195_f_nor;
}
switch(FEAT_TRAINS, SELF, sw_195_t_man, cargo_subtype){
0: spriteset_195_t_nor;
}

switch(FEAT_TRAINS, SELF, sw_195_f, livery_choice){
1: sw_195_f_man;
sw_195_f_auto;
}
switch(FEAT_TRAINS, SELF, sw_195_t, livery_choice){
1: sw_195_t_man;
sw_195_t_auto;
}
//Headlights
switch(FEAT_TRAINS, SELF, sw_195_hl_1, position_in_consist){
0: spriteset_195_hl;
gfx_blank;
}
switch(FEAT_TRAINS, SELF, sw_195_hl_2, position_in_consist_from_end){
0: spriteset_195_rl;
sw_195_hl_1;
}

switch(FEAT_TRAINS, SELF, sw_195_default, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0: return sw_195_f;
	1: return sw_195_hl_2;
}
switch(FEAT_TRAINS, SELF, sw_195_t_default, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0: return sw_195_t;
}

random_switch(FEAT_TRAINS, SELF, sw_195_nor_colour_mapping, TRIGGER_VEHICLE_SERVICE){1: palette_2cc(COLOUR_DARK_BLUE, COLOUR_LIGHT_BLUE) ;}

switch(FEAT_TRAINS, SELF, sw_195_recolour_auto, current_year){
sw_195_nor_colour_mapping;
}
switch(FEAT_TRAINS, SELF, sw_195_recolour_man, cargo_subtype){
0: sw_195_nor_colour_mapping;
}
switch(FEAT_TRAINS, SELF, sw_195_manual_refit_text, cargo_subtype){
0: string(STR_LIVERY_NORTHERN);
return CB_RESULT_NO_TEXT;
}
switch(FEAT_TRAINS, SELF, sw_195_livery_text_decider, livery_choice){
	1: sw_195_manual_refit_text;
	CB_RESULT_NO_TEXT;
}
switch(FEAT_TRAINS, SELF, sw_195_livery_colour_decider, livery_choice){
	1: sw_195_recolour_man;
	sw_195_recolour_auto;
}
switch(FEAT_TRAINS, SELF, sw_195_colour_mapping, colour_scheme_1){
	1: sw_195_livery_colour_decider;
	palette_2cc(company_colour1, company_colour2);
}

/*
switch(FEAT_TRAINS, SELF, sw_195_start_stop, count_veh_id(9090)){
0: string(STR_MORE_CARRIAGES_NEEDED);
CB_RESULT_NO_TEXT;
}
*/

switch(FEAT_TRAINS, SELF, sw_195_coupling_restrictions, vehicle_type_id) {
195: return CB_RESULT_ATTACH_ALLOW;
item_MU90: return CB_RESULT_ATTACH_ALLOW;
37 : return CB_RESULT_ATTACH_ALLOW;
return string(STR_195_NO_COUPLE);
}

item (FEAT_TRAINS, item_195, 195) {
	property {
		name: 							string(STR_NAME_195);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(2017, 1, 1);
		model_life:						15;
		retire_early:					1;
		vehicle_life:					50;
		reliability_decay:				7;
		refittable_cargo_classes:		CC_PASSENGERS;
		cargo_allow_refit:				[PASS, TOUR];
		loading_speed:					28;
		cost_factor:					68;
		running_cost_factor:			30;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							100 mph;
		misc_flags:						bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		refit_cost:						0;
		track_type:						RAIL;
		ai_special_flag:				AI_FLAG_CARGO;
		power:							1046 hp;
		running_cost_base:				RUNNING_COST_DIESEL;
		dual_headed:					1;
		default_cargo_type:				PASS;
		cargo_capacity:					62;
		weight:							44 ton;
		engine_class:					ENGINE_CLASS_DIESEL;
		tractive_effort_coefficient:	0.29;
		air_drag_coefficient:			0.06;
		length:							8;
		effect_spawn_model_and_powered:	EFFECT_SPAWN_MODEL_ELECTRIC;
		bitmask_vehicle_info:			0;
	}
	graphics {
		can_attach_wagon:					sw_195_coupling_restrictions;
		default: 							sw_195_default;
		purchase:							spriteset_195_purchase;
		create_effect:						vfx_diesel_default;
		colour_mapping:						sw_195_colour_mapping;
		cargo_subtype_text:					sw_195_livery_text_decider;
		purchase_colour_mapping:			sw_195_recolour_auto;
		sound_effect:						sw_sfx_dmu_one;
		running_cost_factor:				return(current_speed == 0 ? 7 : 30) ;
		purchase_running_cost_factor : 30;
		//start_stop:							sw_195_start_stop;
}
		livery_override (9090) {
		cargo_capacity: 80;
		default: sw_195_t_default;
		weight:	28;
		colour_mapping:	sw_195_colour_mapping;
		cargo_subtype_text: sw_195_livery_text_decider;
		power: 523;
}
}

if (vehicle_selection < 2){
item (FEAT_TRAINS, item_195, 195){
	property{
		climates_available             : NO_CLIMATE ;
	}
}
}


if (alt_names == 1){
item (FEAT_TRAINS, item_195, 195){
	property{
		name                           : string(STR_NAME_195_ALT) ;
	}
}
}