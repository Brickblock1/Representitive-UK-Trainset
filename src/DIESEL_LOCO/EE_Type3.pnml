//EE Type 3

//Templates

template template_ee3(x,y){
	[x+0,y,8,23,-3,-11] //North
	[x+10,y,20,16,-14,-8] //North East
	[x+32,y,33,13,-17,-9] //East
	[x+67,y,20,16,-5,-8] //South East
	[x+89,y,8,23,-3,-11] //South
	[x+99,y,20,16,-14,-8] //South West
	[x+121,y,33,13,-17,-9] //West
	[x+156,y,20,16,-5,-8] //North West
}

template template_ee3_reverse(x,y){
	[x+89,y,8,23,-3,-11] //South
	[x+99,y,20,16,-14,-8] //South West
	[x+121,y,33,13,-17,-9] //West
	[x+156,y,20,16,-5,-8] //North West
	[x+0,y,8,23,-3,-11] //North
	[x+10,y,20,16,-14,-8] //North East
	[x+32,y,33,13,-17,-9] //East
	[x+67,y,20,16,-5,-8] //South East
}

template template_ee3_dirt(x,y, filename){
	[x+0,y,16,46,-6,-22, filename] //North
	[x+20,y,40,32,-28,-16, filename] //North East
	[x+64,y,66,26,-34,-18, filename] //East
	[x+134,y,40,32,-11,-16, filename] //South East
	[x+178,y,16,46,-6,-22, filename] //South
	[x+198,y,40,32,-27,-16, filename] //South West
	[x+242,y,66,26,-34,-18, filename] //West
	[x+312,y,40,32,-10,-16, filename] //North West
}

template template_ee3_dirt_reverse(x,y, filename){
	[x+178,y,16,46,-6,-22, filename] //South
	[x+198,y,40,32,-27,-16, filename] //South West
	[x+242,y,66,26,-34,-18, filename] //West
	[x+312,y,40,32,-10,-16, filename] //North West
	[x+0,y,16,46,-6,-22, filename] //North
	[x+20,y,40,32,-28,-16, filename] //North East
	[x+64,y,66,26,-34,-18, filename] //East
	[x+134,y,40,32,-11,-16, filename] //South East
}

//Large logo colour mapping

random_switch(FEAT_TRAINS, SELF, sw_ee3_largelogo_colours, TRIGGER_VEHICLE_SERVICE){
	2 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	1 : palette_2cc(COLOUR_GREY, COLOUR_WHITE) ;
}


//Spritesets

spriteset(gfx_ee3_brgreen             , "gfx/DIESEL_LOCO/class_37.png"){template_ee3(0,0)}
spriteset(gfx_ee3_brgreen_reverse     , "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(0,0)}
spriteset(gfx_ee3_brblue              , "gfx/DIESEL_LOCO/class_37.png"){template_ee3(0,25)}
spriteset(gfx_ee3_brblue_reverse      , "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(0,25)}
spriteset(gfx_ee3_brlargelogo         , "gfx/DIESEL_LOCO/class_37.png"){template_ee3(0,50)}
spriteset(gfx_ee3_brlargelogo_reverse , "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(0,50)}
spriteset(gfx_ee3_regional            , "gfx/DIESEL_LOCO/class_37.png"){template_ee3(0,75)}
spriteset(gfx_ee3_regional_reverse    , "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(0,75)}
spriteset(gfx_ee3_railfreight         , "gfx/DIESEL_LOCO/class_37.png"){template_ee3(0,100)}
spriteset(gfx_ee3_railfreight_reverse , "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(0,100)}
spriteset(gfx_ee3_drs                 , "gfx/DIESEL_LOCO/class_37.png"){template_ee3(0,125)}
spriteset(gfx_ee3_drs_reverse         , "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(0,125)}
spriteset(gfx_ee3_ews                 , "gfx/DIESEL_LOCO/class_37.png"){template_ee3(0,150)}
spriteset(gfx_ee3_ews_reverse         , "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(0,150)}
spriteset(gfx_ee3_rog                 , "gfx/DIESEL_LOCO/class_37.png"){template_ee3(0,175)}
spriteset(gfx_ee3_rog_reverse         , "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(0,175)}
spriteset(gfx_ee3_drscompass          , "gfx/DIESEL_LOCO/class_37.png"){template_ee3(0,200)}
spriteset(gfx_ee3_drscompass_reverse  , "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(0,200)}
spriteset(gfx_ee3_colasrail           , "gfx/DIESEL_LOCO/class_37.png"){template_ee3(0,225)}
spriteset(gfx_ee3_colasrail_reverse   , "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(0,225)}

spriteset(gfx_ee3_headcode_brgreen_split          , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(0,0)}
spriteset(gfx_ee3_headcode_brgreen_centre         , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(0,25)}
spriteset(gfx_ee3_headcode_split                  , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(0,50)}
spriteset(gfx_ee3_headcode_split_headlight        , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(178,50)}
spriteset(gfx_ee3_headcode_split_taillight        , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(356,50)}
spriteset(gfx_ee3_headcode_centre                 , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(0,75)}
spriteset(gfx_ee3_headcode_none                   , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(0,100)}
spriteset(gfx_ee3_headcode_centre_headlight       , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(178,75)}
spriteset(gfx_ee3_headcode_centre_taillight       , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(356,75)}
spriteset(gfx_ee3_headcode_none_newlights         , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(0,125)}
spriteset(gfx_ee3_headcode_centre_newlights       , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(0,150)}
spriteset(gfx_ee3_headcode_newlights_headlight    , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(178,125)}
spriteset(gfx_ee3_headcode_newlights_taillight    , "gfx/FIXTURES/class_37_headcodes.png"){template_ee3(356,125)}

spriteset(gfx_ee3_railfreight_black       	, "gfx/DIESEL_LOCO/class_37.png"){template_ee3(235,100)}
spriteset(gfx_ee3_railfreight_black_reverse	, "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(235,100)}
spriteset(gfx_ee3_railfreight_blue          , "gfx/DIESEL_LOCO/class_37.png"){template_ee3(413,100)}
spriteset(gfx_ee3_railfreight_blue_reverse  , "gfx/DIESEL_LOCO/class_37.png"){template_ee3_reverse(413,100)}

spriteset(gfx_ee3_purchase_brgreen          , "gfx/DIESEL_LOCO/class_37.png"){template_purchase_short(178,0)}
spriteset(gfx_ee3_purchase_brblue           , "gfx/DIESEL_LOCO/class_37.png"){template_purchase_short(178,25)}
spriteset(gfx_ee3_purchase_brlargelogo      , "gfx/DIESEL_LOCO/class_37.png"){template_purchase_short(178,50)}
spriteset(gfx_ee3_purchase_regional         , "gfx/DIESEL_LOCO/class_37.png"){template_purchase_short(178,75)}
spriteset(gfx_ee3_purchase_ews              , "gfx/DIESEL_LOCO/class_37.png"){template_purchase_short(178,150)}
spriteset(gfx_ee3_purchase_colasrailfreight , "gfx/DIESEL_LOCO/class_37.png"){template_purchase_short(178,225)}

spriteset(gfx_ee3_dirt_light                , "gfx/FIXTURES/class_37_dirt.png"){base_32bpp()}
spriteset(gfx_ee3_dirt_light_reverse        , "gfx/FIXTURES/class_37_dirt.png"){base_32bpp()}
spriteset(gfx_ee3_dirt_dark                 , "gfx/FIXTURES/class_37_dirt.png"){base_32bpp()}
spriteset(gfx_ee3_dirt_dark_reverse         , "gfx/FIXTURES/class_37_dirt.png"){base_32bpp()}

alternative_sprites(gfx_ee3_dirt_light                , ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP, "gfx/FIXTURES/class_37_dirt.png"){template_ee3_dirt(0,0,"gfx/FIXTURES/class_37_dirt.png")}
alternative_sprites(gfx_ee3_dirt_light_reverse        , ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP, "gfx/FIXTURES/class_37_dirt.png"){template_ee3_dirt(0,0,"gfx/FIXTURES/class_37_dirt.png")}
alternative_sprites(gfx_ee3_dirt_dark                 , ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP, "gfx/FIXTURES/class_37_dirt.png"){template_ee3_dirt(0,48,"gfx/FIXTURES/class_37_dirt.png")}
alternative_sprites(gfx_ee3_dirt_dark_reverse         , ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP, "gfx/FIXTURES/class_37_dirt.png"){template_ee3_dirt(0,48,"gfx/FIXTURES/class_37_dirt.png")}



/*
BR Green
BR Blue
BR Large Logo
Regional Railways
Railfreight
DRS
EWS
ROG
DRS Compass
Colas Railfreight
*/

//Reversal checks

switch(FEAT_TRAINS, PARENT, sw_ee3_brgreen_reverse_check		  , vehicle_is_reversed){ 1 : gfx_ee3_brgreen_reverse ; gfx_ee3_brgreen ; }
switch(FEAT_TRAINS, PARENT, sw_ee3_brblue_reverse_check			  , vehicle_is_reversed){ 1 : gfx_ee3_brblue_reverse ; gfx_ee3_brblue ; }
switch(FEAT_TRAINS, PARENT, sw_ee3_brlargelogo_reverse_check	  , vehicle_is_reversed){ 1 : gfx_ee3_brlargelogo_reverse ; gfx_ee3_brlargelogo ; }
switch(FEAT_TRAINS, PARENT, sw_ee3_regional_reverse_check         , vehicle_is_reversed){ 1 : gfx_ee3_regional_reverse ; gfx_ee3_regional ; }
switch(FEAT_TRAINS, PARENT, sw_ee3_railfreight_reverse_check      , vehicle_is_reversed){ 1 : gfx_ee3_railfreight_reverse ; gfx_ee3_railfreight ; }
switch(FEAT_TRAINS, PARENT, sw_ee3_drs_reverse_check              , vehicle_is_reversed){ 1 : gfx_ee3_drs_reverse ; gfx_ee3_drs ; }
switch(FEAT_TRAINS, PARENT, sw_ee3_ews_reverse_check              , vehicle_is_reversed){ 1 : gfx_ee3_ews_reverse ; gfx_ee3_ews ; }
switch(FEAT_TRAINS, PARENT, sw_ee3_rog_reverse_check              , vehicle_is_reversed){ 1 : gfx_ee3_rog_reverse ; gfx_ee3_rog ; }
switch(FEAT_TRAINS, PARENT, sw_ee3_drscompass_reverse_check       , vehicle_is_reversed){ 1 : gfx_ee3_drscompass_reverse ; gfx_ee3_drscompass ; }
switch(FEAT_TRAINS, PARENT, sw_ee3_colasrailfreight_reverse_check , vehicle_is_reversed){ 1 : gfx_ee3_colasrail_reverse ; gfx_ee3_colasrail ; }

switch(FEAT_TRAINS, PARENT, sw_ee3_railfreight_black_reverse_check  , vehicle_is_reversed){ 1 : gfx_ee3_railfreight_black_reverse ; gfx_ee3_railfreight_black ; }
switch(FEAT_TRAINS, PARENT, sw_ee3_railfreight_blue_reverse_check   , vehicle_is_reversed){ 1 : gfx_ee3_railfreight_blue_reverse ; gfx_ee3_railfreight_blue ; }

switch(FEAT_TRAINS, PARENT, sw_ee3_dirt_light_reverse_check , vehicle_is_reversed){ 1 : gfx_ee3_dirt_light_reverse ; gfx_ee3_dirt_light ; }
switch(FEAT_TRAINS, PARENT, sw_ee3_dirt_dark_reverse_check  , vehicle_is_reversed){ 1 : gfx_ee3_dirt_dark_reverse  ; gfx_ee3_dirt_dark  ; }

switch(FEAT_TRAINS, SELF, sw_ee3_lights_split_back   , position_in_consist_from_end){ 0 : gfx_ee3_headcode_split_taillight ; }
switch(FEAT_TRAINS, SELF, sw_ee3_lights_split_front  , position_in_consist){          0 : gfx_ee3_headcode_split_headlight ; sw_ee3_lights_split_back ; }
switch(FEAT_TRAINS, SELF, sw_ee3_lights_centre_back  , position_in_consist_from_end){ 0 : gfx_ee3_headcode_centre_taillight ; }
switch(FEAT_TRAINS, SELF, sw_ee3_lights_centre_front , position_in_consist){          0 : gfx_ee3_headcode_centre_headlight ; sw_ee3_lights_centre_back ; }
switch(FEAT_TRAINS, SELF, sw_ee3_lights_new_back     , position_in_consist_from_end){ 0 : gfx_ee3_headcode_newlights_taillight ; }
switch(FEAT_TRAINS, SELF, sw_ee3_lights_new_front    , position_in_consist){          0 : gfx_ee3_headcode_newlights_headlight ; sw_ee3_lights_new_back ; }





//Graphics

//Base



switch(FEAT_TRAINS, SELF, sw_ee3_sector_gfx_decider, most_common_cargo_type){
	PASS : sw_ee3_regional_reverse_check ;
	COAL : sw_ee3_railfreight_black_reverse_check ;
	OIL_ : sw_ee3_railfreight_blue_reverse_check ;
	STEL : sw_ee3_railfreight_blue_reverse_check ;
	PLAS : sw_ee3_railfreight_blue_reverse_check ;
	BRCK : sw_ee3_railfreight_blue_reverse_check ;
	CMNT : sw_ee3_railfreight_blue_reverse_check ;
	GOLD : sw_ee3_railfreight_blue_reverse_check ;
	BDMT : sw_ee3_railfreight_blue_reverse_check ;
	GLAS : sw_ee3_railfreight_blue_reverse_check ;
	PETR : sw_ee3_railfreight_blue_reverse_check ;
	RFPR : sw_ee3_railfreight_blue_reverse_check ;
	SAND : sw_ee3_railfreight_blue_reverse_check ;
	ENSP : sw_ee3_railfreight_blue_reverse_check ;
	GRVL : sw_ee3_railfreight_blue_reverse_check ;
	MNSP : sw_ee3_railfreight_blue_reverse_check ;
	SCMT : sw_ee3_railfreight_blue_reverse_check ;
	COPR : sw_ee3_railfreight_blue_reverse_check ;
	sw_ee3_railfreight_reverse_check ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_privatisation1_random, TRIGGER_VEHICLE_SERVICE){
	2 : sw_ee3_drs_reverse_check ;
	1 : sw_ee3_ews_reverse_check ;
}

switch(FEAT_TRAINS, PARENT, sw_ee3_privatisation2_decider, most_common_cargo_type){
	PASS : sw_ee3_drscompass_reverse_check ;
	URAN : sw_ee3_drscompass_reverse_check ;
	NWST : sw_ee3_drscompass_reverse_check ;
	GOOD : sw_ee3_drscompass_reverse_check ;
	MAIL : sw_ee3_drscompass_reverse_check ;
	VALU : sw_ee3_drscompass_reverse_check ;
	GOLD : sw_ee3_drscompass_reverse_check ;
	FOOD : sw_ee3_drscompass_reverse_check ;
	STEL : sw_ee3_colasrailfreight_reverse_check ;
	COAL : sw_ee3_colasrailfreight_reverse_check ;
	IORE : sw_ee3_colasrailfreight_reverse_check ;
	LVST : sw_ee3_colasrailfreight_reverse_check ;
	OIL_ : sw_ee3_colasrailfreight_reverse_check ;
	PAPR : sw_ee3_colasrailfreight_reverse_check ;
	WOOD : sw_ee3_colasrailfreight_reverse_check ;
	CORE : sw_ee3_colasrailfreight_reverse_check ;
	AORE : sw_ee3_colasrailfreight_reverse_check ;
	BDMT : sw_ee3_colasrailfreight_reverse_check ;
	BRCK : sw_ee3_colasrailfreight_reverse_check ;
	CMNT : sw_ee3_colasrailfreight_reverse_check ;
	GLAS : sw_ee3_colasrailfreight_reverse_check ;
	PETR : sw_ee3_colasrailfreight_reverse_check ;
	PLAS : sw_ee3_colasrailfreight_reverse_check ;
	RFPR : sw_ee3_colasrailfreight_reverse_check ;
	SAND : sw_ee3_colasrailfreight_reverse_check ;
	CLAY : sw_ee3_colasrailfreight_reverse_check ;
	GRVL : sw_ee3_colasrailfreight_reverse_check ;
	COPR : sw_ee3_colasrailfreight_reverse_check ;
	sw_ee3_rog_reverse_check ;
}

switch(FEAT_TRAINS, SELF, sw_ee3_gfx_auto, build_year){
	ERA_BRCREST : sw_ee3_brgreen_reverse_check ;
	1967..1982  : sw_ee3_brblue_reverse_check ;
	1987..1995  : sw_ee3_sector_gfx_decider ;
	1996..2008  : sw_ee3_privatisation1_random ;
	2009..4096  : sw_ee3_privatisation2_decider ;
	sw_ee3_brlargelogo_reverse_check ;
}



//Manual


switch(FEAT_TRAINS, SELF, sw_ee3_gfx_manual, cargo_subtype){
	0  : sw_ee3_brgreen_reverse_check ;
	1  : sw_ee3_brblue_reverse_check ;
	4  : sw_ee3_regional_reverse_check ;
	5  : sw_ee3_railfreight_reverse_check ;
	6  : sw_ee3_railfreight_black_reverse_check ;
	7  : sw_ee3_railfreight_blue_reverse_check ;
	8  : sw_ee3_drs_reverse_check ;
	9  : sw_ee3_drscompass_reverse_check ;
	10 : sw_ee3_ews_reverse_check ;
	11 : sw_ee3_rog_reverse_check ;
	12 : sw_ee3_colasrailfreight_reverse_check ;
	sw_ee3_brlargelogo_reverse_check ;
}


switch(FEAT_TRAINS, SELF, sw_ee3_gfx_manual_check, livery_choice){
	1 : sw_ee3_gfx_manual ;
	sw_ee3_gfx_auto ;
}






//Headcodes

//Random switches

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxhc_brgreen_random, TRIGGER_VEHICLE_SERVICE){
	2 : gfx_ee3_headcode_brgreen_split ;
	1 : gfx_ee3_headcode_brgreen_centre ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxhc_standard_random, TRIGGER_VEHICLE_SERVICE){
	2 : gfx_ee3_headcode_split ;
	1 : gfx_ee3_headcode_centre ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxhc_standard_3, TRIGGER_VEHICLE_SERVICE){
	1 : gfx_ee3_headcode_none ;
	1 : gfx_ee3_headcode_centre ;
	1 : gfx_ee3_headcode_split ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxhc_standard_any, TRIGGER_VEHICLE_SERVICE){
	1 : gfx_ee3_headcode_split ;
	1 : gfx_ee3_headcode_centre ;
	1 : gfx_ee3_headcode_none ;
	1 : gfx_ee3_headcode_none_newlights ;
	1 : gfx_ee3_headcode_centre_newlights ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxhc_newlights_random, TRIGGER_VEHICLE_SERVICE){
	1 : gfx_ee3_headcode_none_newlights ;
	1 : gfx_ee3_headcode_centre_newlights ;
}



//Auto

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxhc_largelogo, TRIGGER_VEHICLE_SERVICE){
	dependent : sw_ee3_largelogo_colours ;
	2 : sw_ee3_gfxhc_standard_random ;
	1 : sw_ee3_gfxhc_standard_3 ;
}

switch(FEAT_TRAINS, SELF, sw_ee3_gfxhc_sector_decider, most_common_cargo_type){
	PASS : gfx_ee3_headcode_centre ;
	sw_ee3_gfxhc_standard_3 ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxhc_privatisation1_decider, TRIGGER_VEHICLE_SERVICE){
	dependent : sw_ee3_privatisation1_random ;
	2 : sw_ee3_gfxhc_standard_any ;
	1 : sw_ee3_gfxhc_standard_3 ;
}

switch(FEAT_TRAINS, PARENT, sw_ee3_gfxhc_privatisation2_decider, most_common_cargo_type){
	PASS : sw_ee3_gfxhc_standard_any ;
	URAN : sw_ee3_gfxhc_standard_any ;
	NWST : sw_ee3_gfxhc_standard_any ;
	GOOD : sw_ee3_gfxhc_standard_any ;
	MAIL : sw_ee3_gfxhc_standard_any ;
	VALU : sw_ee3_gfxhc_standard_any ;
	GOLD : sw_ee3_gfxhc_standard_any ;
	FOOD : sw_ee3_gfxhc_standard_any ;
	STEL : sw_ee3_gfxhc_standard_3 ;
	COAL : sw_ee3_gfxhc_standard_3 ;
	IORE : sw_ee3_gfxhc_standard_3 ;
	LVST : sw_ee3_gfxhc_standard_3 ;
	OIL_ : sw_ee3_gfxhc_standard_3 ;
	PAPR : sw_ee3_gfxhc_standard_3 ;
	WOOD : sw_ee3_gfxhc_standard_3 ;
	CORE : sw_ee3_gfxhc_standard_3 ;
	AORE : sw_ee3_gfxhc_standard_3 ;
	BDMT : sw_ee3_gfxhc_standard_3 ;
	BRCK : sw_ee3_gfxhc_standard_3 ;
	CMNT : sw_ee3_gfxhc_standard_3 ;
	GLAS : sw_ee3_gfxhc_standard_3 ;
	PETR : sw_ee3_gfxhc_standard_3 ;
	PLAS : sw_ee3_gfxhc_standard_3 ;
	RFPR : sw_ee3_gfxhc_standard_3 ;
	SAND : sw_ee3_gfxhc_standard_3 ;
	CLAY : sw_ee3_gfxhc_standard_3 ;
	GRVL : sw_ee3_gfxhc_standard_3 ;
	COPR : sw_ee3_gfxhc_standard_3 ;
	sw_ee3_gfxhc_newlights_random ;
}


switch(FEAT_TRAINS, SELF, sw_ee3_gfxhc_auto, build_year){
	ERA_BRCREST : sw_ee3_gfxhc_brgreen_random ;
	1983..1986  : sw_ee3_gfxhc_largelogo ;
	1987..1995  : sw_ee3_gfxhc_sector_decider ;
	1996..2008  : sw_ee3_gfxhc_privatisation1_decider ;
	2009..4096  : sw_ee3_gfxhc_privatisation2_decider ;
	sw_ee3_gfxhc_standard_random ;
}


//Manual

switch(FEAT_TRAINS, SELF, sw_ee3_gfxhc_manual, cargo_subtype){
	0    : sw_ee3_gfxhc_brgreen_random ;
	4    : gfx_ee3_headcode_centre ;
	5..7 : sw_ee3_gfxhc_standard_3 ;
	8..9 : sw_ee3_gfxhc_standard_any ;
	10   : sw_ee3_gfxhc_standard_3 ;
	11   : sw_ee3_gfxhc_newlights_random ;
	12   : sw_ee3_gfxhc_standard_3 ;
	sw_ee3_gfxhc_standard_random ;
}

switch(FEAT_TRAINS, SELF, sw_ee3_gfxhc_manual_check, livery_choice){
	1 : sw_ee3_gfxhc_manual ;
	sw_ee3_gfxhc_auto ;
}




//Lights

//Random switches

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxlt_brgreen_random, TRIGGER_VEHICLE_SERVICE){
	dependent : sw_ee3_gfxhc_brgreen_random ;
	2 : sw_ee3_lights_split_front ;
	1 : sw_ee3_lights_centre_front ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxlt_standard_random, TRIGGER_VEHICLE_SERVICE){
	dependent : sw_ee3_gfxhc_standard_random ;
	2 : sw_ee3_lights_split_front ;
	1 : sw_ee3_lights_centre_front ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxlt_standard_3, TRIGGER_VEHICLE_SERVICE){
	dependent : sw_ee3_gfxhc_standard_3 ;
	1 : sw_ee3_lights_centre_front ;
	1 : sw_ee3_lights_centre_front ;
	1 : sw_ee3_lights_split_front ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxlt_standard_any, TRIGGER_VEHICLE_SERVICE){
	dependent : sw_ee3_gfxhc_standard_any ;
	1 : sw_ee3_lights_split_front ;
	1 : sw_ee3_lights_centre_front ;
	1 : sw_ee3_lights_centre_front ;
	1 : sw_ee3_lights_new_front ;
	1 : sw_ee3_lights_new_front ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxlt_newlights, TRIGGER_VEHICLE_SERVICE){
	dependent : sw_ee3_gfxhc_newlights_random ;
	1 : sw_ee3_lights_new_front ;
	1 : sw_ee3_lights_new_front ;
}


//Auto

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxlt_largelogo, TRIGGER_VEHICLE_SERVICE){
	dependent : sw_ee3_largelogo_colours ;
	2 : sw_ee3_gfxlt_standard_random ;
	1 : sw_ee3_gfxlt_standard_3 ;
}

switch(FEAT_TRAINS, SELF, sw_ee3_gfxlt_sector_decider, most_common_cargo_type){
	PASS : sw_ee3_lights_centre_front ;
	sw_ee3_gfxlt_standard_3 ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_gfxlt_privatisation1_decider, TRIGGER_VEHICLE_SERVICE){
	dependent : sw_ee3_privatisation1_random ;
	2 : sw_ee3_gfxlt_standard_any ;
	1 : sw_ee3_gfxlt_standard_3 ;
}

switch(FEAT_TRAINS, PARENT, sw_ee3_gfxlt_privatisation2_decider, most_common_cargo_type){
	PASS : sw_ee3_gfxlt_standard_any ;
	URAN : sw_ee3_gfxlt_standard_any ;
	NWST : sw_ee3_gfxlt_standard_any ;
	GOOD : sw_ee3_gfxlt_standard_any ;
	MAIL : sw_ee3_gfxlt_standard_any ;
	VALU : sw_ee3_gfxlt_standard_any ;
	GOLD : sw_ee3_gfxlt_standard_any ;
	FOOD : sw_ee3_gfxlt_standard_any ;
	STEL : sw_ee3_gfxlt_standard_3 ;
	COAL : sw_ee3_gfxlt_standard_3 ;
	IORE : sw_ee3_gfxlt_standard_3 ;
	LVST : sw_ee3_gfxlt_standard_3 ;
	OIL_ : sw_ee3_gfxlt_standard_3 ;
	PAPR : sw_ee3_gfxlt_standard_3 ;
	WOOD : sw_ee3_gfxlt_standard_3 ;
	CORE : sw_ee3_gfxlt_standard_3 ;
	AORE : sw_ee3_gfxlt_standard_3 ;
	BDMT : sw_ee3_gfxlt_standard_3 ;
	BRCK : sw_ee3_gfxlt_standard_3 ;
	CMNT : sw_ee3_gfxlt_standard_3 ;
	GLAS : sw_ee3_gfxlt_standard_3 ;
	PETR : sw_ee3_gfxlt_standard_3 ;
	PLAS : sw_ee3_gfxlt_standard_3 ;
	RFPR : sw_ee3_gfxlt_standard_3 ;
	SAND : sw_ee3_gfxlt_standard_3 ;
	CLAY : sw_ee3_gfxlt_standard_3 ;
	GRVL : sw_ee3_gfxlt_standard_3 ;
	COPR : sw_ee3_gfxlt_standard_3 ;
	sw_ee3_gfxlt_privatisation1_decider ;
}


switch(FEAT_TRAINS, SELF, sw_ee3_gfxlt_auto, build_year){
	ERA_BRCREST : sw_ee3_gfxlt_brgreen_random ;
	1983..1986  : sw_ee3_gfxlt_largelogo ;
	1987..1995  : sw_ee3_gfxlt_sector_decider ;
	1996..2008  : sw_ee3_gfxlt_privatisation1_decider ;
	2009..4096  : sw_ee3_gfxlt_privatisation2_decider ;
	sw_ee3_gfxlt_standard_random ;
}



//Manual

switch(FEAT_TRAINS, SELF, sw_ee3_gfxlt_manual, cargo_subtype){
	0    : sw_ee3_gfxlt_brgreen_random ;
	4    : sw_ee3_lights_centre_front ;
	5..7 : sw_ee3_gfxlt_standard_3 ;
	8..9 : sw_ee3_gfxlt_standard_any ;
	10   : sw_ee3_gfxlt_standard_3 ;
	11   : sw_ee3_gfxlt_newlights ;
	12   : sw_ee3_gfxlt_standard_3 ;
	sw_ee3_gfxlt_standard_random ;
}


switch(FEAT_TRAINS, SELF, sw_ee3_gfxlt_manual_check, livery_choice){
	1 : sw_ee3_gfxlt_manual ;
	sw_ee3_gfxlt_auto ;
}



//Dirt

switch(FEAT_TRAINS, SELF, sw_ee3_dirt, age_in_days){
	0..3649    : gfx_blank ;
	3650..9124 : sw_ee3_dirt_light_reverse_check ;
	sw_ee3_dirt_dark_reverse_check ;
}


//Sprite stacking

switch(FEAT_TRAINS, SELF, sw_ee3_spritestack, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0 : sw_ee3_gfx_manual_check ;    //Base
	1 : sw_ee3_gfxhc_manual_check ;  //Headcodes
	2 : sw_ee3_gfxlt_manual_check ;  //Lights
	3 : sw_ee3_dirt ;                //Dirt
}





//Refit text

switch(FEAT_TRAINS, SELF, sw_ee3_refit_text, cargo_subtype){
	0 : string(STR_LIVERY_BRGREEN) ;
	1 : string(STR_LIVERY_BRBLUE) ;
	2 : string(STR_LIVERY_BRBLUE_LOGO) ;
	3 : string(STR_LIVERY_BRRAILFREIGHT_LOGO) ;
	4 : string(STR_LIVERY_REG_RAILWAYS) ;
	5 : return(string(STR_LIVERY_RAILFREIGHT,string(STR_SECTOR_GENERAL))) ;
	6 : return(string(STR_LIVERY_RAILFREIGHT,string(STR_SECTOR_COAL))) ;
	7 : return(string(STR_LIVERY_RAILFREIGHT,string(STR_SECTOR_METALS))) ;
	8 : string(STR_LIVERY_DRS) ;
	9 : string(STR_LIVERY_DRS_COMPASS) ;
	10 : string(STR_LIVERY_EWS) ;
	11 : string(STR_LIVERY_ROG_EUROPHENIX) ;
	12 : string(STR_LIVERY_COLAS) ;
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_ee3_refit_text_check, livery_choice){
	1 : sw_ee3_refit_text ;
	CB_RESULT_NO_TEXT ;
}


//Purchase sprite

switch(FEAT_TRAINS, SELF, sw_ee3_purchase, current_year){
	ERA_BRCREST : gfx_ee3_purchase_brgreen ;
	1967..1982  : gfx_ee3_purchase_brblue ;
	1983..1986  : gfx_ee3_purchase_brlargelogo ;
	1987..1995  : gfx_ee3_purchase_regional ;
	1996..2008  : gfx_ee3_purchase_ews ;
	2009..4096  : gfx_ee3_purchase_colasrailfreight ;
}


//Colour mapping

switch(FEAT_TRAINS, SELF, sw_ee3_colours_sectorised, most_common_cargo_type){
	PASS : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
}

random_switch(FEAT_TRAINS, SELF, sw_ee3_colours_privatisation1_decider, TRIGGER_VEHICLE_SERVICE){
	dependent : sw_ee3_privatisation1_random ;
	2 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	1 : palette_2cc(COLOUR_RED, COLOUR_YELLOW) ;
}

switch(FEAT_TRAINS, PARENT, sw_ee3_colours_privatisation2_decider, most_common_cargo_type){
	PASS : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	URAN : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	NWST : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	GOOD : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	MAIL : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	VALU : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	GOLD : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	FOOD : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	STEL : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	COAL : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	IORE : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	LVST : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	OIL_ : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	PAPR : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	WOOD : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	CORE : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	AORE : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	BDMT : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	BRCK : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	CMNT : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	GLAS : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	PETR : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	PLAS : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	RFPR : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	SAND : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	CLAY : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	GRVL : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	COPR : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	palette_2cc(COLOUR_GREY, COLOUR_RED) ;
}

switch(FEAT_TRAINS, SELF, sw_ee3_colours_auto, build_year){
	ERA_BRCREST : palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN) ;
	1967..1982  : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	1983..1986  : sw_ee3_largelogo_colours ;
	1987..1995  : sw_ee3_colours_sectorised ;
	1996..2008  : sw_ee3_colours_privatisation1_decider ;
	2009..4096  : sw_ee3_colours_privatisation2_decider ;
}



switch(FEAT_TRAINS, SELF, sw_ee3_colours_manual, cargo_subtype){
	0    : palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN) ;
	1..2 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	3    : palette_2cc(COLOUR_GREY, COLOUR_WHITE) ;
	4    : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	5..7 : palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	8..9 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	10   : palette_2cc(COLOUR_RED, COLOUR_YELLOW) ;
	11   : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	12   : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_ee3_colours_manual_check, livery_choice){
	1 : sw_ee3_colours_manual ;
	sw_ee3_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_ee3_colours_2cc_check, colour_scheme_1){
	1 : sw_ee3_colours_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}


switch(FEAT_TRAINS, SELF, sw_ee3_colours_purchase, current_year){
	ERA_BRCREST : palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN) ;
	1967..1982  : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	1983..1986  : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	1987..1995  : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	1996..2008  : palette_2cc(COLOUR_RED, COLOUR_YELLOW) ;
	2009..4096  : palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
}


//Item

item (FEAT_TRAINS, item_eetype3, 37){
	property{
		name                           : string(STR_NAME_EETYPE3) ;  
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date(1959,12,2) ;
		refit_cost                     : 0 ;
		refittable_cargo_classes       : 0 ;
		cargo_allow_refit              : [GOOD] ;
		cargo_capacity                 : 1 ;
		model_life                     : 30 ;
		vehicle_life                   : 50 ;
		reliability_decay              : 10 ;
		cost_factor                    : 55 ;
		running_cost_base              : RUNNING_COST_DIESEL ;
		running_cost_factor            : 170 ;
		speed                          : 80 mph ;
		track_type                     : RAIL ;
		power                          : 1750 hp ;
		weight                         : 100 ton ;
		engine_class                   : ENGINE_CLASS_DIESEL ;
		tractive_effort_coefficient    : 0.245 ;
		air_drag_coefficient           : 0.1 ;
		length                         : 8 ;
		misc_flags                     : bitmask(TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC) ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_DIESEL ;
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		dual_headed                    : 0 ;
	}
	graphics{
		default                        : sw_ee3_spritestack ;
		colour_mapping                 : sw_ee3_colours_2cc_check ;
		purchase_colour_mapping        : sw_ee3_colours_purchase ;
		purchase                       : sw_ee3_purchase ;
		sound_effect                   : sw_sfx_diesel_loco_ee_2 ;
		create_effect                  : vfx_diesel_default ;
		cargo_subtype_text             : sw_ee3_refit_text_check ;
		purchase_cargo_capacity        : 0 ;
		cargo_capacity                 : return 0 ;
		can_attach_wagon               : sw_all_but_MU90;
	}
}