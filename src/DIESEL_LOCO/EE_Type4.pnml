//Defining EE Type 4 specific template

template template_eetype4(x,y){
	//left_x, upper_y, width, height, offset_x, offset_y
	[x+0,y,8,23,-3,-10]    //North
	[x+10,y,21,15,-14,-7]  //North East
	[x+33,y,33,11,-17,-7]  //East
	[x+69,y,21,15,-5,-7]   //South East
	[x+92,y,8,23,-3,-10]   //South
	[x+102,y,21,15,-14,-7]   //South West
	[x+125,y,33,11,-17,-7] //West
	[x+161,y,21,15,-5,-7]  //North West
}
//Reverse
template template_eetype4_reverse(x,y){
	//left_x, upper_y, width, height, offset_x, offset_y
	[x+92,y,8,23,-3,-10]   //South
	[x+102,y,21,15,-14,-7]   //South West
	[x+125,y,33,11,-17,-7] //West
	[x+161,y,21,15,-5,-7]  //North West
	[x+0,y,8,23,-3,-10]    //North
	[x+10,y,21,15,-14,-7]  //North East
	[x+33,y,33,11,-17,-7]  //East
	[x+69,y,21,15,-5,-7]   //South East}
}


//Defining graphics
spriteset (eetype4_headcode_1,  "gfx/FIXTURES/class_40_hc1.png"){template_eetype4(0,0)}        //Headcode 1
spriteset (eetype4_headcode_2,  "gfx/FIXTURES/class_40_hc2.png"){template_eetype4(0,0)}        //Headcode 2
spriteset (eetype4_headcode_3,  "gfx/FIXTURES/class_40_hc3.png"){template_eetype4(0,0)}        //Headcode 3
spriteset (eetype4_headcode_4,  "gfx/FIXTURES/class_40_hc4.png"){template_eetype4(0,0)}        //Headcode 4
spriteset (eetype4_headcode_5,  "gfx/FIXTURES/class_40_hc5.png"){template_eetype4(0,0)}        //Headcode 5
spriteset (eetype4_br_blue,     "gfx/DIESEL_LOCO/class_40.png"){template_eetype4(0,0)}         //BR Blue
spriteset (eetype4_br_blue_rev, "gfx/DIESEL_LOCO/class_40.png"){template_eetype4_reverse(0,0)} //BR Blue Reverse

//Sound switch
switch(FEAT_TRAINS, SELF, sw_eetype4_sfx, getbits(extra_callback_info1, 0, 8)){
	SOUND_EVENT_STOPPED       :sound("sfx/40_idle.wav");
    SOUND_EVENT_VISUAL_EFFECT :sound("sfx/40_run.wav");
    SOUND_EVENT_RUNNING       :sound("sfx/40_coast.wav");
    SOUND_EVENT_START         :sound("sfx/40_horn.wav");
    SOUND_EVENT_BREAKDOWN     :SOUND_TRAIN_BREAKDOWN;
	SOUND_EVENT_TUNNEL        :sound("sfx/40_horn.wav");
}

//Livery callback
switch(FEAT_TRAINS, SELF, sw_eetype4_livery, build_year){
	ERA_BRCREST       : eetype4_br_blue; //BR Green
	ERA_BRBLUE        : eetype4_br_blue; //BR Blue
	ERA_BRBLUETOPS    : eetype4_br_blue; //BR Blue
	ERA_SECTORISATION : eetype4_br_blue; //BR Large Logo
	ERA_PRIVATISATION : eetype4_br_blue; //BR Green
	eetype4_br_blue;                     //BR Blue
}

//Reverse livery callback
switch(FEAT_TRAINS, SELF, sw_eetype4_livery_reverse, build_year){
	ERA_BRCREST       : eetype4_br_blue_rev; //BR Green
	ERA_BRBLUE        : eetype4_br_blue_rev; //BR Blue
	ERA_BRBLUETOPS    : eetype4_br_blue_rev; //BR Blue
	ERA_SECTORISATION : eetype4_br_blue_rev; //BR Large Logo
	ERA_PRIVATISATION : eetype4_br_blue_rev; //BR Green
	eetype4_br_blue_rev;                     //BR Blue
}

//Reverse callback
switch(FEAT_TRAINS, SELF, sw_eetype4_reverse, vehicle_is_reversed){
	1: sw_eetype4_livery_reverse;
	sw_eetype4_livery;
}

//Headcode randomisation
random_switch(FEAT_TRAINS, SELF, sw_eetype4_headcode, TRIGGER_VEHICLE_SERVICE){
	3:eetype4_headcode_1;
	3:eetype4_headcode_2;
	4:eetype4_headcode_3;
	2:eetype4_headcode_4;
	1:eetype4_headcode_5;
}

//Stacked sprites merging
switch(FEAT_TRAINS, SELF, sw_eetype4_default, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0: sw_eetype4_reverse;
	1: sw_eetype4_headcode;
}

//2cc colour mapping livery switch
switch (FEAT_TRAINS, SELF, sw_eetype4_colour_mapping_colours, build_year){
	ERA_BRCREST       : palette_2cc(COLOUR_DARK_GREEN, COLOUR_GREEN);    //BR Green
	ERA_BRBLUE        : palette_2cc(COLOUR_DARK_BLUE, COLOUR_DARK_BLUE); //BR Blue
	ERA_BRBLUETOPS    : palette_2cc(COLOUR_DARK_BLUE, COLOUR_DARK_BLUE); //BR Blue
	ERA_SECTORISATION : palette_2cc(COLOUR_DARK_BLUE, COLOUR_YELLOW);    //BR Large Logo
	ERA_PRIVATISATION : palette_2cc(COLOUR_DARK_GREEN, COLOUR_GREEN);    //BR Green
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_DARK_BLUE);                     //BR Blue
}

//2cc colourmapping
switch (FEAT_TRAINS, SELF, sw_eetype4_colour_mapping, colour_scheme_1){
	0: palette_2cc(company_colour1, company_colour2);
	1: palette_2cc(COLOUR_DARK_BLUE, COLOUR_DARK_BLUE);
}


//EE Type 4 item

item (FEAT_TRAINS, item_eetype4){
	property{
		name                         : string(STR_NAME_EETYPE4);  
		climates_available           : ALL_CLIMATES;
		introduction_date            : date(1958,1,1);
		model_life                   : 27;
		vehicle_life                 : 35;
		reliability_decay            : 9;
		cost_factor                  : 60;
		running_cost_base            : RUNNING_COST_DIESEL;
		running_cost_factor          : 200;
		speed                        : 90 mph;
		power                        : 2000 hp;
		weight                       : 134 ton;
		engine_class                 : ENGINE_CLASS_DIESEL;
		tractive_effort_coefficient  : 0.1723880597014925;
		air_drag_coefficient         : 0.2;
		length                       : 8;
		misc_flags                   : bitmask(TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC);
		sprite_id                    : SPRITE_ID_NEW_TRAIN;
	}
	graphics{
		default        : sw_eetype4_default;
		colour_mapping : sw_eetype4_colour_mapping;
		purchase       : eetype4_br_blue;
		sound_effect   : sw_eetype4_sfx;
	}
}