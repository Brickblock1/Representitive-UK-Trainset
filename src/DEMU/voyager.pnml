//Voyager


//Sprite sets

spriteset(gfx_voyager_virgin_middle		, "gfx/DEMU/voyajah.png"	){	template_8_4_networker		( 000, 225)}
spriteset(gfx_voyager_virgin_front		, "gfx/DEMU/voyajah.png"	){	template_8_8_networker		( 000, 200)}

spriteset(gfx_voyager_arriva_middle		, "gfx/DEMU/voyajah.png"	){	template_8_4_networker		( 000, 275)}
spriteset(gfx_voyager_arriva_front		, "gfx/DEMU/voyajah.png"	){	template_8_8_networker		( 000, 250)}

spriteset(gfx_voyager_avanti_middle		, "gfx/DEMU/voyajah.png"	){	template_8_4_networker		( 358, 325)}
spriteset(gfx_voyager_avanti_front		, "gfx/DEMU/voyajah.png"	){	template_8_8_networker		( 358, 300)}

spriteset(gfx_meridian_mml_middle		, "gfx/DEMU/voyajah.png"	){	template_8_4_networker		( 000, 325)}
spriteset(gfx_meridian_mml_front		, "gfx/DEMU/voyajah.png"	){	template_8_8_networker		( 000, 300)}

spriteset(gfx_meridian_emt_middle		, "gfx/DEMU/voyajah.png"	){	template_8_4_networker		( 358, 275)}
spriteset(gfx_meridian_emt_front		, "gfx/DEMU/voyajah.png"	){	template_8_8_networker		( 358, 250)}

spriteset(gfx_voyager_virgin_middle_2cc	, "gfx/DEMU/voyajah.png"	){	template_8_4_networker		( 179, 225)}
spriteset(gfx_voyager_virgin_front_2cc	, "gfx/DEMU/voyajah.png"	){	template_8_8_networker		( 179, 200)}

spriteset(gfx_voyager_arriva_middle_2cc	, "gfx/DEMU/voyajah.png"	){	template_8_4_networker		( 179, 275)}
spriteset(gfx_voyager_arriva_front_2cc	, "gfx/DEMU/voyajah.png"	){	template_8_8_networker		( 179, 250)}

spriteset(gfx_voyager_virgin_purchase	, "gfx/DEMU/voyajah.png"	){	template_dualhead_purchase	( 269, 225)}
spriteset(gfx_voyager_arriva_purchase	, "gfx/DEMU/voyajah.png"	){	template_dualhead_purchase	( 269, 275)}

spriteset(gfx_voyager_headlight			, "gfx/DEMU/voyajah.png"	){	template_8_8_networker		( 358, 200)}
spriteset(gfx_voyager_taillight			, "gfx/DEMU/voyajah.png"	){	template_8_8_networker		( 358, 225)}


//Switches

switch(FEAT_TRAINS, SELF, sw_voyager_virgin_2cc, colour_scheme_1	){ 1 : gfx_voyager_virgin_front ; gfx_voyager_virgin_front_2cc ; }
switch(FEAT_TRAINS, SELF, sw_voyager_virgin_t_2cc, colour_scheme_1	){ 1 : gfx_voyager_virgin_middle ; gfx_voyager_virgin_middle_2cc ; }

switch(FEAT_TRAINS, SELF, sw_voyager_arriva_2cc, colour_scheme_1	){ 1 : gfx_voyager_arriva_front ; gfx_voyager_arriva_front_2cc ; }
switch(FEAT_TRAINS, SELF, sw_voyager_arriva_t_2cc, colour_scheme_1	){ 1 : gfx_voyager_arriva_middle ; gfx_voyager_arriva_middle_2cc ; }

//Liveries

switch(FEAT_TRAINS, SELF, sw_voyager_auto, current_year < 2009){
	1 : sw_voyager_virgin_2cc ;
	sw_voyager_arriva_2cc ;
}

switch(FEAT_TRAINS, SELF, sw_voyager_manual, cargo_subtype){
	1 : sw_voyager_arriva_2cc ;
	2 : gfx_voyager_avanti_front ;
	3 : gfx_meridian_mml_front ;
	4 : gfx_meridian_emt_front ;
	sw_voyager_virgin_2cc ;
}

switch(FEAT_TRAINS, SELF, sw_voyager_manual_check, livery_choice){
	1 : sw_voyager_manual ;
	sw_voyager_auto ;
}


switch(FEAT_TRAINS, SELF, sw_voyager_headlights, position_in_consist){ 0 : gfx_voyager_headlight ;}
switch(FEAT_TRAINS, SELF, sw_voyager_taillights, position_in_consist_from_end){ 0 : gfx_voyager_taillight ;}

switch(FEAT_TRAINS, SELF, sw_voyager_sprite_stack, SPR_STACK){
	0 : sw_voyager_manual_check ;
	1 : sw_voyager_taillights ;
	2 : sw_voyager_headlights ;
}


//Intermediate carriages

switch(FEAT_TRAINS, SELF, sw_voyager_middle_auto, current_year < 2009){
	1 : sw_voyager_virgin_t_2cc ;
	sw_voyager_arriva_t_2cc ;
}

switch(FEAT_TRAINS, SELF, sw_voyager_middle_manual, cargo_subtype){
	1 : sw_voyager_arriva_t_2cc ;
	2 : gfx_voyager_avanti_middle ;
	3 : gfx_meridian_mml_middle ;
	4 : gfx_meridian_emt_middle ;
	sw_voyager_virgin_t_2cc ;
}

switch(FEAT_TRAINS, SELF, sw_voyager_middle_manual_check, livery_choice){
	1 : sw_voyager_middle_manual ;
	sw_voyager_middle_auto ;
}


//Colour Mapping

switch(FEAT_TRAINS, SELF, sw_voyager_colours_auto, current_year < 2009){
	1 : palette_2cc(COLOUR_RED, COLOUR_GREY) ;
	palette_2cc(COLOUR_PINK, COLOUR_RED) ;
}

switch(FEAT_TRAINS, SELF, sw_voyager_colours_manual, cargo_subtype){
	1 : palette_2cc(COLOUR_PINK, COLOUR_RED) ;
	2 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	3 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	4 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ;
	palette_2cc(COLOUR_RED, COLOUR_GREY) ;
}

switch(FEAT_TRAINS, SELF, sw_voyager_colours_manual_check, livery_choice){
	1 : sw_voyager_colours_manual ;
	sw_voyager_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_voyager_colours_2cc_check, colour_scheme_1){
	1 : sw_voyager_colours_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}



//Purchase sprite

switch(FEAT_TRAINS, SELF, sw_voyager_purchase_sprite, current_year < 2009){
	1 : gfx_voyager_virgin_purchase ;
	gfx_voyager_arriva_purchase ;
}

switch(FEAT_TRAINS, SELF, sw_voyager_purchase_2cc_check, colour_scheme_1){
	1 : sw_voyager_colours_auto ;
	palette_2cc(company_colour1, company_colour2) ;
}



//Refit text

switch(FEAT_TRAINS, SELF, sw_voyager_refit_text, cargo_subtype){
	0 : string(STR_LIVERY_VIRGIN_TRAINS) ;
	1 : string(STR_LIVERY_ARRIVA_XC) ;
	2 : string(STR_LIVERY_AVANTI) ;
	3 : string(STR_LIVERY_MIDLAND_MAINLINE_BLUE) ;
	4 : string(STR_LIVERY_EMT) ;
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_voyager_refit_text_check, livery_choice){
	1 : sw_voyager_refit_text ;
	CB_RESULT_NO_TEXT ;
}



//Item


switch(FEAT_TRAINS, SELF, sw_voyager_attach, vehicle_type_id){
	220 : return CB_RESULT_ATTACH_ALLOW  ;
	item_MU90 : return CB_RESULT_ATTACH_ALLOW  ;
	return string(STR_220_NO_COUPLE) ;
}


item(FEAT_TRAINS, item_voyager, 220){
	property{
		name                           : string(STR_NAME_VOYAGER) ;
		misc_flags                     : bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_SPRITE_STACK);
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		length                         : 8 ;
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date(2001,1,1) ;
		model_life                     : VEHICLE_NEVER_EXPIRES ;
		vehicle_life                   : 35 ;
		reliability_decay              : 9 ;
		cost_factor                    : 100 ;
		running_cost_base              : RUNNING_COST_DIESEL ;
		running_cost_factor            : 180 ;
		refit_cost                     : 0 ;
		refittable_cargo_classes       : CC_PASSENGERS ;
		cargo_allow_refit              : [PASS, TOUR] ;
		cargo_capacity                 : 65 ;
		loading_speed                  : 17 ;
		track_type                     : RAIL ;
		engine_class                   : ENGINE_CLASS_DIESEL ;
		speed                          : 125 mph ;
		power                          : 1120 kW ;
		weight                         : 50 ton ;
		tractive_effort_coefficient    : 0.15 ;
		air_drag_coefficient           : 0.06 ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_DIESEL ;
		dual_headed                    : 1 ;
	}
	graphics{
		default							: sw_voyager_sprite_stack ;
		purchase						: sw_voyager_purchase_sprite ;
		sound_effect					: sw_sfx_dmu_two ;
		create_effect					: vfx_diesel_default ;
		colour_mapping					: sw_voyager_colours_2cc_check ;
		purchase_colour_mapping			: sw_voyager_purchase_2cc_check ;
		cargo_subtype_text				: sw_voyager_refit_text_check ;
		can_attach_wagon				: sw_voyager_attach ;
		running_cost_factor				: return(current_speed == 0 ? 50 : 180) ;
		purchase_running_cost_factor	: 180 ;
	}
	livery_override (item_MU90){
		default							: sw_voyager_middle_manual_check ;
		colour_mapping					: sw_voyager_colours_2cc_check ;
		cargo_capacity					: 70 ;
		power							: 560 ;
		cargo_subtype_text				: sw_voyager_refit_text_check ;
		weight							: 45 ;
		effect_spawn_model_and_powered	: EFFECT_SPAWN_MODEL_DIESEL ;
		create_effect					: vfx_diesel_default ;
	}
}

if (alt_names == 1){
item (FEAT_TRAINS, item_voyager, 220){
	property{
		name                           : string(STR_NAME_VOYAGER_ALT) ;
	}
}
}