//Front Cars
spriteset(spriteset_769_f_gwr, "gfx/DEMU/class_769.png") {template_8_8_networker(0, 200)}
spriteset(spriteset_769_f_tfw, "gfx/DEMU/class_769.png") {template_8_8_networker(179, 500)}
spriteset(spriteset_769_f_nra, "gfx/DEMU/class_769.png") {template_8_8_networker(179, 800)}
//Rear Cars
spriteset(spriteset_769_r_gwr, "gfx/DEMU/class_769.png") {template_8_8_networker(0, 225)}
spriteset(spriteset_769_r_tfw, "gfx/DEMU/class_769.png") {template_8_8_networker(179, 525)}
spriteset(spriteset_769_r_nra, "gfx/DEMU/class_769.png") {template_8_8_networker(179, 825)}
//Med Driving
spriteset(spriteset_769_o_gwr, "gfx/DEMU/class_769.png") {template_8_8_networker(0, 250)}
spriteset(spriteset_769_o_tfw, "gfx/DEMU/class_769.png") {template_8_8_networker(179, 550)}
spriteset(spriteset_769_o_nra, "gfx/DEMU/class_769.png") {template_8_8_networker(179, 850)}
//PMSO
spriteset(spriteset_769_m_gwr, "gfx/DEMU/class_769.png") {template_8_8_networker(0, 175)}
spriteset(spriteset_769_m_tfw, "gfx/DEMU/class_769.png") {template_8_8_networker(179, 475)}
spriteset(spriteset_769_m_nra, "gfx/DEMU/class_769.png") {template_8_8_networker(179, 775)}
//Trailer
spriteset(spriteset_769_t_gwr, "gfx/DEMU/class_769.png") {template_8_8_networker(0, 150)}
spriteset(spriteset_769_t_tfw, "gfx/DEMU/class_769.png") {template_8_8_networker(179, 450)}
spriteset(spriteset_769_t_nra, "gfx/DEMU/class_769.png") {template_8_8_networker(179, 750)}
//Purchase
spriteset(spriteset_769_purchase_gwr, "gfx/DEMU/class_769.png") {template_dualhead_purchase(0, 275)}
spriteset(spriteset_769_purchase_tfw, "gfx/DEMU/class_769.png") {template_dualhead_purchase(179, 575)}
spriteset(spriteset_769_purchase_nra, "gfx/DEMU/class_769.png") {template_dualhead_purchase(179, 875)}
//Panto
spriteset(spriteset_769_panto_up, "gfx/DEMU/class_769.png") {template_8_8_networker(180, 0)}
spriteset(spriteset_769_panto_dn, "gfx/DEMU/class_769.png") {template_8_8_networker(180, 25)}


//Switches

//Power Car
switch(FEAT_TRAINS, SELF, sw_769_gwr_f, position_in_consist){
	0: spriteset_769_f_gwr; spriteset_769_o_gwr;
}
switch(FEAT_TRAINS, SELF, sw_flex_gwr_select, position_in_consist_from_end){
	0: spriteset_769_r_gwr; sw_769_gwr_f;
}
random_switch(FEAT_TRAINS, SELF, sw_flex_gwr, TRIGGER_VEHICLE_SERVICE){1: sw_flex_gwr_select;}
//
switch(FEAT_TRAINS, SELF, sw_769_tfw_f, position_in_consist){
	0: spriteset_769_f_tfw; spriteset_769_o_tfw;
}
switch(FEAT_TRAINS, SELF, sw_flex_tfw_select, position_in_consist_from_end){
	0: spriteset_769_r_tfw; sw_769_tfw_f;
}
random_switch(FEAT_TRAINS, SELF, sw_flex_tfw, TRIGGER_VEHICLE_SERVICE){1: sw_flex_tfw_select;}	
//
switch(FEAT_TRAINS, SELF, sw_769_nra_f, position_in_consist){
	0: spriteset_769_f_nra; spriteset_769_o_nra;
}
switch(FEAT_TRAINS, SELF, sw_flex_nra_select, position_in_consist_from_end){
	0: spriteset_769_r_nra; sw_769_nra_f;
}
random_switch(FEAT_TRAINS, SELF, sw_flex_nra, TRIGGER_VEHICLE_SERVICE){1: sw_flex_nra_select;}
	
	
	
//Trailer/Pan
switch(FEAT_TRAINS, SELF, sw_769_gwr_msb_f, position_in_vehid_chain){
	0: spriteset_769_m_gwr; spriteset_769_t_gwr;
}
switch(FEAT_TRAINS, SELF, sw_769_gwr_msb_r, position_in_vehid_chain_from_end){
	0: spriteset_769_m_gwr; spriteset_769_t_gwr;
}
switch(FEAT_TRAINS, SELF, sw_flex_gwr_select_t, vehicle_is_reversed){
	0: sw_769_gwr_msb_f; 
	sw_769_gwr_msb_r;
}
random_switch(FEAT_TRAINS, SELF, sw_flex_gwr_t, TRIGGER_VEHICLE_SERVICE){1: sw_flex_gwr_select_t;}

switch(FEAT_TRAINS, SELF, sw_769_tfw_msb_f, position_in_vehid_chain){
	0: spriteset_769_m_tfw; spriteset_769_t_tfw;
}
switch(FEAT_TRAINS, SELF, sw_769_tfw_msb_r, position_in_vehid_chain_from_end){
	0: spriteset_769_m_tfw; spriteset_769_t_tfw;
}
switch(FEAT_TRAINS, SELF, sw_flex_tfw_select_t, vehicle_is_reversed){
	0: sw_769_tfw_msb_f; 
	sw_769_tfw_msb_r;
}
random_switch(FEAT_TRAINS, SELF, sw_flex_tfw_t, TRIGGER_VEHICLE_SERVICE){1: sw_flex_tfw_select_t;}

switch(FEAT_TRAINS, SELF, sw_769_nra_msb_f, position_in_vehid_chain){
	0: spriteset_769_m_nra; spriteset_769_t_nra;
}
switch(FEAT_TRAINS, SELF, sw_769_nra_msb_r, position_in_vehid_chain_from_end){
	0: spriteset_769_m_nra; spriteset_769_t_nra;
}
switch(FEAT_TRAINS, SELF, sw_flex_nra_select_t, vehicle_is_reversed){
	0: sw_769_nra_msb_f; 
	sw_769_nra_msb_r;
}
random_switch(FEAT_TRAINS, SELF, sw_flex_nra_t, TRIGGER_VEHICLE_SERVICE){1: sw_flex_nra_select_t;}


//Panto

switch(FEAT_TRAINS, SELF, sw_769_panto_check, STANDARD_GAUGE_CHECK_OHLE_ONLY){
	1: spriteset_769_panto_up ;
	spriteset_769_panto_dn ;
}
switch(FEAT_TRAINS, SELF, sw_769_pan_msb_f, position_in_vehid_chain){
	0: sw_769_panto_check;
}
switch(FEAT_TRAINS, SELF, sw_769_pan_msb_r, position_in_vehid_chain_from_end){
	0: sw_769_panto_check;
}
switch(FEAT_TRAINS, SELF, sw_flex_pan_select_t, vehicle_is_reversed){
	0: sw_769_pan_msb_f; 
	sw_769_pan_msb_r;
}


//Livery Switches 
 
//Manual liveries

switch(FEAT_TRAINS, SELF, sw_769_manual_livery_decider, cargo_subtype){
	0: sw_flex_gwr;
	1: sw_flex_tfw;
	2: sw_flex_nra;
}

switch(FEAT_TRAINS, SELF, sw_769_manual_livery_decider_t, cargo_subtype){
	0: sw_flex_gwr_t;
	1: sw_flex_tfw_t;
	2: sw_flex_nra_t;
}

switch(FEAT_TRAINS, SELF, sw_769_livery_choice_decider, livery_choice){
	1 : sw_769_manual_livery_decider ; 
	sw_flex_gwr ;
}

switch(FEAT_TRAINS, SELF, sw_769_livery_choice_decider_t, livery_choice){
	1 : sw_769_manual_livery_decider_t ; 
	sw_flex_gwr_t ;
}

//Refit text

switch(FEAT_TRAINS, SELF, sw_769_refit_text, cargo_subtype){
	0: string(STR_LIVERY_FIRST_GWR);
	1: string(STR_LIVERY_TFW);
	2: string(STR_LIVERY_NORTHERN_ARRIVA);
	return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_769_refit_text_decider, livery_choice){
	1 : sw_769_refit_text ;
	return CB_RESULT_NO_TEXT ;
}

//Sprite stack

switch(FEAT_TRAINS, SELF, sw_769_default, SPR_STACK){
	0 : return sw_769_livery_choice_decider;
}

switch(FEAT_TRAINS, SELF, sw_769_default_t, SPR_STACK){
	0 : return sw_769_livery_choice_decider_t;
	1 : return sw_flex_pan_select_t;
}

//Recolour

random_switch(FEAT_TRAINS, SELF, sw_769_tfw_colour_mapping, TRIGGER_VEHICLE_SERVICE){ 
	1 : palette_2cc(COLOUR_RED, company_colour2) ;
}

random_switch(FEAT_TRAINS, SELF, sw_769_gwr_colour_mapping, TRIGGER_VEHICLE_SERVICE){ 
	1 : palette_2cc(COLOUR_DARK_GREEN, COLOUR_GREY) ;
}

random_switch(FEAT_TRAINS, SELF, sw_769_nra_colour_mapping, TRIGGER_VEHICLE_SERVICE){ 
	1 : palette_2cc(COLOUR_DARK_BLUE, company_colour2) ;
}
//Recolour Switches


switch(FEAT_TRAINS, SELF, sw_769_colour_mapping_manual, cargo_subtype){
	0: sw_769_gwr_colour_mapping;
	1: sw_769_tfw_colour_mapping;
	2: sw_769_nra_colour_mapping;
	return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, PARENT, sw_769_colour_mapping_manual_check, livery_choice){
	1 : sw_769_colour_mapping_manual ;
	sw_769_gwr_colour_mapping ;
}

switch(FEAT_TRAINS, PARENT, sw_769_colour_mapping_decider, colour_scheme_1){
	1 : sw_769_colour_mapping_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}

switch(FEAT_TRAINS, PARENT, sw_769_colour_mapping_purchase, colour_scheme_1){
	1 : sw_769_gwr_colour_mapping ;
	palette_2cc(company_colour1, company_colour2) ;
}

switch(FEAT_TRAINS, SELF, sw_769_coupling_restrictions, vehicle_type_id) {
    9030: return CB_RESULT_ATTACH_ALLOW;
	769: return CB_RESULT_ATTACH_ALLOW;
	37 : return CB_RESULT_ATTACH_ALLOW;
    return string(STR_769_NO_COUPLE);
}

switch(FEAT_TRAINS, PARENT, sw_769_power, STANDARD_GAUGE_CHECK_EVERY_ELEC){
	1 : 1438 ;
	1046 ;
}



//Effects

switch(FEAT_TRAINS, SELF, sw_769_sfx_check, STANDARD_GAUGE_CHECK_EVERY_ELEC){
	1 : sw_sfx_emu_gec ;
	sw_sfx_dmu_one ;
}


switch(FEAT_TRAINS, SELF, sw_769_vfx_check_2, STANDARD_GAUGE_CHECK_THIRD_ONLY){
	1 : vfx_3rd ;
	vfx_diesel_default ;
}

switch(FEAT_TRAINS, SELF, sw_769_vfx_check_1, STANDARD_GAUGE_CHECK_OHLE_ONLY){
	1 : vfx_ohle ;
	sw_769_vfx_check_2 ;
}


switch(FEAT_TRAINS, SELF, sw_769_effect_1_check, STANDARD_GAUGE_CHECK_EVERY_ELEC){
	1 : EFFECT_SPAWN_MODEL_ELECTRIC ;
	EFFECT_SPAWN_MODEL_DIESEL ;
}





item (FEAT_TRAINS, item_769, 769) {
	property {
		name: 							string(STR_NAME_769);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(2019, 1, 1);
		model_life:						VEHICLE_NEVER_EXPIRES;
		retire_early:					1;
		vehicle_life:					50;
		reliability_decay:				7;
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		cargo_allow_refit:				[PASS, TOUR];
		loading_speed:					23;
		cost_factor:					45;
		running_cost_factor:			34;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							100 mph;
		misc_flags:						bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_2CC);
		refit_cost:						0;
		track_type:						RAIL;
		ai_special_flag:				AI_FLAG_PASSENGER;
		power:							1438 hp;
		running_cost_base:				RUNNING_COST_DIESEL;
		dual_headed:					1;
		default_cargo_type:				PASS;
		cargo_capacity:					75;
		weight:							30 ton;
		tractive_effort_coefficient:	0.1;
		air_drag_coefficient:			0.06;
		length:							8;
		effect_spawn_model_and_powered:	EFFECT_SPAWN_MODEL_DIESEL;
		bitmask_vehicle_info:			0;
		cargo_age_period:				185;
		default_cargo_type:				PASS;
		engine_class:					ENGINE_CLASS_DIESEL;
	}
	graphics {
		can_attach_wagon:					sw_769_coupling_restrictions;
		default: 							sw_769_default;
		purchase:							spriteset_769_purchase_gwr;
		create_effect:						sw_769_vfx_check_1;
		cargo_subtype_text:					sw_769_refit_text_decider ;
		colour_mapping:						sw_769_colour_mapping_decider;
		purchase_colour_mapping: 			sw_769_colour_mapping_purchase;
		running_cost_factor: return(current_speed == 0 ? 8 : 34) ;
		purchase_running_cost_factor:		34;
		sound_effect:						sw_769_sfx_check ;
		power:								sw_769_power;
		effect_spawn_model_and_powered:		sw_769_effect_1_check;
}
		livery_override (9030) {
		cargo_capacity: 85;
		default: sw_769_default_t;
		colour_mapping:	sw_769_colour_mapping_decider;
		cargo_subtype_text: sw_769_refit_text_decider ;
}
}

if (vehicle_selection < 3){
item (FEAT_TRAINS, item_769, 769){
	property{
		climates_available             : NO_CLIMATE ;
	}
}
}


if (alt_names == 1){
item (FEAT_TRAINS, item_769, 769){
	property{
		name                           : string(STR_NAME_769_ALT) ;
	}
}
}
