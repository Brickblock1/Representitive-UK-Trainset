spriteset(spriteset_700_tl_f, "gfx/EMU/class_707.png") {
	template_8_8_networker(0, 200)
}

spriteset(spriteset_700_tl_r, "gfx/EMU/class_707.png") {
	template_8_8_networker(0, 225)
}

spriteset(spriteset_700_tl_o, "gfx/EMU/class_707.png") {
	template_8_8_networker(0, 250)
}

spriteset(spriteset_700_tl_t, "gfx/EMU/class_707.png") {
	template_8_8_networker(0, 150)
}

spriteset(spriteset_700_tl_p, "gfx/EMU/class_707.png") {
	template_8_8_networker(0, 175)
}

spriteset(spriteset_700_swt_f, "gfx/EMU/class_707.png") {
	template_8_8_networker(0, 350)
}

spriteset(spriteset_700_swt_r, "gfx/EMU/class_707.png") {
	template_8_8_networker(0, 375)
}

spriteset(spriteset_700_swt_o, "gfx/EMU/class_707.png") {
	template_8_8_networker(0, 400)
}

spriteset(spriteset_700_swt_t, "gfx/EMU/class_707.png") {
	template_8_8_networker(0, 300)
}

spriteset(spriteset_700_purchase, "gfx/EMU/class_707.png") {
	template_dualhead_purchase(0, 275)
}

//Switches


switch(FEAT_TRAINS, SELF, sw_700_front, position_in_consist){
	0: spriteset_700_tl_f;
	spriteset_700_tl_o;
}
switch(FEAT_TRAINS, SELF, sw_700_tl_select, position_in_consist_from_end){
	0: spriteset_700_tl_r;
	return sw_700_front;
}
switch(FEAT_TRAINS, SELF, sw_700_tl_t_select, position_in_vehid_chain){
	0: spriteset_700_tl_p;
	spriteset_700_tl_t;
}
random_switch(FEAT_TRAINS, SELF, sw_700, TRIGGER_VEHICLE_SERVICE){1:sw_700_tl_select;}
random_switch(FEAT_TRAINS, SELF, sw_700_t, TRIGGER_VEHICLE_SERVICE){1: sw_700_tl_t_select;}

switch(FEAT_TRAINS, SELF, sw_707_front, position_in_consist){
	0: spriteset_700_swt_f;
	spriteset_700_swt_o;
}
switch(FEAT_TRAINS, SELF, sw_707_swt_select, position_in_consist_from_end){
	0: spriteset_700_swt_r;
	return sw_707_front;
}
switch(FEAT_TRAINS, SELF, sw_707_swt_t_select, position_in_vehid_chain){
	0: spriteset_700_swt_t;
	spriteset_700_swt_t;
}
random_switch(FEAT_TRAINS, SELF, sw_707, TRIGGER_VEHICLE_SERVICE){1:sw_707_swt_select;}
random_switch(FEAT_TRAINS, SELF, sw_707_t, TRIGGER_VEHICLE_SERVICE){1: sw_707_swt_t_select;}

//How the livery is selected

switch(FEAT_TRAINS, PARENT, sw_700_livery_selection_auto, (count_veh_id(item_MU90)/count_veh_id(item_700))){
	0..1: sw_707;
	 sw_700;
} 

switch(FEAT_TRAINS, PARENT, sw_700_livery_selection_auto_t, (count_veh_id(item_MU90)/count_veh_id(item_700))){
	0..1: sw_707_t;
	 sw_700_t;
} 

switch(FEAT_TRAINS, SELF, sw_700_livery_selection_manual, cargo_subtype){
	0: sw_700;
	1: sw_707;
} 

switch(FEAT_TRAINS, PARENT, sw_700_livery_selection_manual_t, cargo_subtype){
	0: sw_700_t;
	1: sw_707_t;
} 

switch(FEAT_TRAINS, SELF, sw_700_livery_selection, livery_choice){
	1: sw_700_livery_selection_manual;
	sw_700_livery_selection_auto;
}

switch(FEAT_TRAINS, SELF, sw_700_livery_selection_t, livery_choice){
	1: sw_700_livery_selection_manual_t;
	sw_700_livery_selection_auto_t;
}

switch(FEAT_TRAINS, SELF, sw_700_default, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0 : return sw_700_livery_selection;
}

switch(FEAT_TRAINS, SELF, sw_700_trailer, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0 : return sw_700_livery_selection_t;
}

//Refit text

switch(FEAT_TRAINS, SELF, sw_700_refit_text, cargo_subtype){
	0: string(STR_LIVERY_THAMESLINK);
	1: string(STR_LIVERY_SWT);
	return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_700_refit_text_decider, livery_choice){
	1 : sw_700_refit_text ;
	return CB_RESULT_NO_TEXT ;
}

//Recolour options

random_switch(FEAT_TRAINS, SELF, sw_707_colour_mapping, TRIGGER_VEHICLE_SERVICE){
	1: palette_2cc(COLOUR_RED, COLOUR_DARK_BLUE) ;
}

random_switch(FEAT_TRAINS, SELF, sw_700_tl_colour_mapping, TRIGGER_VEHICLE_SERVICE){
	1: palette_2cc(COLOUR_LIGHT_BLUE, company_colour2) ;
}

//Recolour decicsion

switch(FEAT_TRAINS, PARENT, sw_700_recolour_selection_auto, (count_veh_id(item_MU90)/count_veh_id(item_700))){
	0..1: sw_707_colour_mapping;
	 sw_700_tl_colour_mapping;
} 

switch(FEAT_TRAINS, SELF, sw_700_recolour_selection_manual, cargo_subtype){
	0: sw_700_tl_colour_mapping;
	1: sw_707_colour_mapping;
}

switch(FEAT_TRAINS, SELF, sw_700_recolour_livery_selection, livery_choice){
	1: sw_700_recolour_selection_manual;
	sw_700_recolour_selection_auto;
}

switch(FEAT_TRAINS, PARENT, sw_700_colour_mapping, colour_scheme_1){
	1 : sw_700_recolour_livery_selection ;
	palette_2cc(company_colour1, company_colour2) ;
}

switch(FEAT_TRAINS, SELF, sw_700_coupling_restrictions, vehicle_type_id) {
    item_MU90: return CB_RESULT_ATTACH_ALLOW;
	item_700: return CB_RESULT_ATTACH_ALLOW;
	37 : return CB_RESULT_ATTACH_ALLOW;
    return string(STR_700_NO_COUPLE);
}

item (FEAT_TRAINS, item_700, 700) {
	property {
		name: 							string(STR_NAME_700);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(2015, 1, 1);
		model_life:						15;
		retire_early:					1;
		vehicle_life:					50;
		reliability_decay:				7;
		refittable_cargo_classes:		CC_PASSENGERS;
		cargo_allow_refit:				[PASS, TOUR];
		loading_speed:					35;
		cost_factor:					75;
		running_cost_factor:			60;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							100 mph;
		misc_flags:						bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_2CC);
		refit_cost:						0;
		track_type:						SAAZ;
		ai_special_flag:				AI_FLAG_CARGO;
		power:							4425 hp;
		running_cost_base:				RUNNING_COST_ELECTRIC;
		dual_headed:					1;
		default_cargo_type:				PASS;
		cargo_capacity:					90;
		weight:							34 ton;
		engine_class:					ENGINE_CLASS_ELECTRIC;
		tractive_effort_coefficient:	0.11;
		air_drag_coefficient:			0.06;
		length:							8;
		effect_spawn_model_and_powered:	EFFECT_SPAWN_MODEL_ELECTRIC;
		bitmask_vehicle_info:			0;
	}
	graphics {
		can_attach_wagon:					sw_700_coupling_restrictions;
		default: 							sw_700_default;
		purchase:							spriteset_700_purchase;
		create_effect:						vfx_dual_voltage;
		colour_mapping:						sw_700_colour_mapping;
		cargo_subtype_text:					sw_700_refit_text_decider;
		purchase_colour_mapping:			sw_700_colour_mapping;
		sound_effect:						sw_sfx_emu_desiro;
		running_cost_factor:				return(current_speed == 0 ? 19 : 60) ;
}
		livery_override (item_MU90) {
		cargo_capacity: 90;
		power: 200;
		default: sw_700_trailer;
		weight:	34;
		colour_mapping:	sw_700_colour_mapping;
		cargo_subtype_text: sw_700_refit_text_decider;
}
}