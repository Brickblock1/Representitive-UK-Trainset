//Head car outside
spriteset(spriteset_395_d, "gfx/EMU/class_395.png") {template_8_8_networker(0, 0)}
spriteset(spriteset_iet_d_hitachi, "gfx/EMU/class_395.png") {template_8_8_networker(0, 75)}
spriteset(spriteset_iet_d_gwr, "gfx/EMU/class_395.png") {template_8_8_networker(0, 150)}
spriteset(spriteset_iet_d_tpe, "gfx/EMU/class_395.png") {template_8_8_networker(0, 225)}
spriteset(spriteset_iet_d_vtec, "gfx/EMU/class_395.png") {template_8_8_networker(0, 300)}
spriteset(spriteset_iet_d_lner, "gfx/EMU/class_395.png") {template_8_8_networker(0, 375)}
//Head car inside
spriteset(spriteset_395_i, "gfx/EMU/class_395.png") {template_8_8_networker(0, 25)}
spriteset(spriteset_iet_i_hitachi, "gfx/EMU/class_395.png") {template_8_8_networker(0, 100)}
spriteset(spriteset_iet_i_gwr, "gfx/EMU/class_395.png") {template_8_8_networker(0, 175)}
spriteset(spriteset_iet_i_tpe, "gfx/EMU/class_395.png") {template_8_8_networker(0, 250)}
spriteset(spriteset_iet_i_vtec, "gfx/EMU/class_395.png") {template_8_8_networker(0, 325)}
spriteset(spriteset_iet_i_lner, "gfx/EMU/class_395.png") {template_8_8_networker(0, 400)}
//Trailer car
spriteset(spriteset_395_t, "gfx/EMU/class_395.png") {template_8_4_networker(0, 50)}
spriteset(spriteset_iet_t_hitachi, "gfx/EMU/class_395.png") {template_8_4_networker(0, 125)}
spriteset(spriteset_iet_t_gwr, "gfx/EMU/class_395.png") {template_8_4_networker(0, 200)}
spriteset(spriteset_iet_t_tpe, "gfx/EMU/class_395.png") {template_8_4_networker(0, 275)}
spriteset(spriteset_iet_t_vtec, "gfx/EMU/class_395.png") {template_8_4_networker(0, 350)}
spriteset(spriteset_iet_t_lner, "gfx/EMU/class_395.png") {template_8_4_networker(0, 425)}
//Purchase sprite
spriteset(spriteset_395_purchase, "gfx/EMU/class_395.png") {template_dualhead_purchase(90, 50)}
spriteset(spriteset_iet_purchase, "gfx/EMU/class_395.png") {template_dualhead_purchase(90, 125)}
//Pantograph & lights
spriteset(spriteset_hitachi_pan_up, "gfx/FIXTURES/hitachi_hs_fixtures.png") {template_8_8_networker(0, 0)}
spriteset(spriteset_hitachi_pan_down, "gfx/FIXTURES/hitachi_hs_fixtures.png") {template_8_8_networker(0, 25)}
spriteset(spriteset_395_lights_f, "gfx/FIXTURES/hitachi_hs_fixtures.png") {template_8_8_networker(0, 50)}
spriteset(spriteset_395_lights_r, "gfx/FIXTURES/hitachi_hs_fixtures.png") {template_8_8_networker(0, 75)}
spriteset(spriteset_iet_lights_f, "gfx/FIXTURES/hitachi_hs_fixtures.png") {template_8_8_networker(0, 100)}
spriteset(spriteset_iet_lights_r, "gfx/FIXTURES/hitachi_hs_fixtures.png") {template_8_8_networker(0, 125)}

switch(FEAT_TRAINS, PARENT, sw_hitachi_pan, current_railtype){
ELRL: spriteset_hitachi_pan_up;
spriteset_hitachi_pan_down;
}

switch(FEAT_TRAINS, SELF, sw_395_coupling2, num_vehs_in_vehid_chain){
1: spriteset_395_d;
spriteset_395_i;
}

switch(FEAT_TRAINS, SELF, sw_395_coupling, (num_vehs_in_consist/count_veh_id(395))){
1: spriteset_395_d;
sw_395_coupling2;
}

switch(FEAT_TRAINS, PARENT, sw_395_start_stop2, count_veh_id(395)/num_vehs_in_consist){
1: string(STR_MORE_CARRIAGES_NEEDED);
CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_395_start_stop, num_vehs_in_vehid_chain){
0..2: sw_395_start_stop2;
string(STR_MORE_CARRIAGES_NEEDED);
}

switch(FEAT_TRAINS, SELF, sw_395_lights2, position_in_consist){
0: spriteset_395_lights_f;
gfx_blank;
}

switch(FEAT_TRAINS, SELF, sw_395_lights, position_in_consist_from_end){
0: spriteset_395_lights_r;
sw_395_lights2;
}

switch(FEAT_TRAINS, SELF, sw_395_default, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 3 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0: return sw_395_coupling;
	1: return sw_395_lights;
	2: sw_hitachi_pan;
}

switch(FEAT_TRAINS, SELF, sw_395_refit_text, cargo_subtype){
	0: string(STR_LIVERY_SE_BLUE);
	return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_395_livery_decider, livery_choice){
	1: sw_395_refit_text;
	return CB_RESULT_NO_TEXT;
}

random_switch(FEAT_TRAINS, SELF, sw_395_colour_mapping, TRIGGER_VEHICLE_SERVICE){1: palette_2cc(COLOUR_MAUVE, COLOUR_MAUVE) ;}

switch(FEAT_TRAINS, SELF, sw_395_colour_auto, current_year){
sw_395_colour_mapping;
}

switch(FEAT_TRAINS, SELF, sw_395_colour_man, cargo_subtype){
	0: sw_395_colour_mapping;
}

switch(FEAT_TRAINS, SELF, sw_395_colour_livery_decider, livery_choice){
	1: sw_395_colour_man;
	sw_395_colour_auto;
}

switch(FEAT_TRAINS, SELF, sw_395_recolour, colour_scheme_1){
	1: sw_395_colour_livery_decider;
	palette_2cc(company_colour1, company_colour2);
}

switch(FEAT_TRAINS, SELF, sw_395_coupling_restrictions, vehicle_type_id) {
    9090: return CB_RESULT_ATTACH_ALLOW;
	395: return CB_RESULT_ATTACH_ALLOW;
	37 : return CB_RESULT_ATTACH_ALLOW;
    return string(STR_395_NO_COUPLE);
}

switch(FEAT_TRAINS, SELF, sw_395_speed, current_railtype) {
    ELRL: 225;
    SAAZ: 225;
	160;
}

item (FEAT_TRAINS, item_395, 395) {
	property {
		name: 							string(STR_NAME_395);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(2009, 1, 1);
		model_life:						20;
		retire_early:					1;
		vehicle_life:					30;
		reliability_decay:				7;
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		cargo_allow_refit:				[PASS, TOUR];
		loading_speed:					20;
		cost_factor:					60;
		running_cost_factor:			35;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							140 mph;
		misc_flags:						bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		refit_cost:						0;
		track_type:						SAAZ;
		ai_special_flag:				AI_FLAG_CARGO;
		power:							1120 hp;
		running_cost_base:				RUNNING_COST_ELECTRIC;
		dual_headed:					1;
		default_cargo_type:				PASS;
		cargo_capacity:					40;
		weight:							47 ton;
		engine_class:					ENGINE_CLASS_ELECTRIC;
		tractive_effort_coefficient:	0.45;
		air_drag_coefficient:			0.07;
		length:							8;
		effect_spawn_model_and_powered:	EFFECT_SPAWN_MODEL_ELECTRIC;
		bitmask_vehicle_info:			0;
		cargo_age_period:				250;
		default_cargo_type:				PASS;
	}
	graphics {
		can_attach_wagon:					sw_395_coupling_restrictions;
		default: 							sw_395_default;
		purchase:							spriteset_395_purchase;
		cargo_subtype_text:					sw_395_livery_decider ;
		colour_mapping:						sw_395_recolour ;
		purchase_colour_mapping:			sw_395_recolour ;
		sound_effect:						sw_sfx_electric_loco_modern ;
		speed:								sw_395_speed;
		running_cost_factor:				return(current_speed == 0 ? 8 : 35) ;
		purchase_running_cost_factor : 35;
		start_stop:							sw_395_start_stop;
}
		livery_override (item_MU90) {
		cargo_capacity: 66;
		default: spriteset_395_t;
		colour_mapping:	sw_395_recolour;
		cargo_subtype_text: sw_395_livery_decider ;
		weight:							45;
		power:							560;
}
}

if (vehicle_selection < 2){
item (FEAT_TRAINS, item_395, 395){
	property{
		climates_available             : NO_CLIMATE ;
	}
}
}

if (alt_names == 1){
item (FEAT_TRAINS, item_395, 395){
	property{
		name                           : string(STR_NAME_395_ALT) ;
	}
}
}

//IET code
//Note: it is coded as a DMU because reasons
switch(FEAT_TRAINS, SELF, sw_iet_d_manual, cargo_subtype){
0: spriteset_iet_d_hitachi;
1: spriteset_iet_d_gwr;
2: spriteset_iet_d_vtec;
3: spriteset_iet_d_lner;
4: spriteset_iet_d_tpe;
}

switch(FEAT_TRAINS, SELF, sw_iet_d, livery_choice){
	1: sw_iet_d_manual;
	return spriteset_iet_d_hitachi;
}

switch(FEAT_TRAINS, SELF, sw_iet_i_manual, cargo_subtype){
0: spriteset_iet_i_hitachi;
1: spriteset_iet_i_gwr;
2: spriteset_iet_i_vtec;
3: spriteset_iet_i_lner;
4: spriteset_iet_i_tpe;
}

switch(FEAT_TRAINS, SELF, sw_iet_i, livery_choice){
	1: sw_iet_i_manual;
	return spriteset_iet_i_hitachi;
}

switch(FEAT_TRAINS, SELF, sw_iet_t_manual, cargo_subtype){
0: spriteset_iet_t_hitachi;
1: spriteset_iet_t_gwr;
2: spriteset_iet_t_vtec;
3: spriteset_iet_t_lner;
4: spriteset_iet_t_tpe;
}

switch(FEAT_TRAINS, SELF, sw_iet_t, livery_choice){
	1: sw_iet_t_manual;
	return spriteset_iet_t_hitachi;
}

switch(FEAT_TRAINS, SELF, sw_iet_coupling2, num_vehs_in_vehid_chain){
1: sw_iet_d;
sw_iet_i;
}

switch(FEAT_TRAINS, SELF, sw_iet_coupling, (num_vehs_in_consist/count_veh_id(800))){
1: sw_iet_d;
sw_iet_coupling2;
}

switch(FEAT_TRAINS, PARENT, sw_iet_start_stop2, count_veh_id(800)/num_vehs_in_consist){
1: string(STR_MORE_CARRIAGES_NEEDED);
CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_iet_start_stop, num_vehs_in_vehid_chain){
0..2: sw_iet_start_stop2;
string(STR_MORE_CARRIAGES_NEEDED);
}

switch(FEAT_TRAINS, SELF, sw_iet_lights2, position_in_consist){
0: spriteset_iet_lights_f;
gfx_blank;
}

switch(FEAT_TRAINS, SELF, sw_iet_lights, position_in_consist_from_end){
0: spriteset_iet_lights_r;
sw_iet_lights2;
}

switch(FEAT_TRAINS, SELF, sw_iet_default, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 3 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0: return sw_iet_coupling;
	1: return sw_iet_lights;
	2: sw_hitachi_pan;
}

switch(FEAT_TRAINS, SELF, sw_iet_refit_text, cargo_subtype){
	0: string(STR_LIVERY_HITACHI);
	1: string(STR_LIVERY_FIRST_GWR);
	2: string(STR_LIVERY_VTEC);
	3: string(STR_LIVERY_LNER);
	4: string(STR_LIVERY_TPE_NEW);
	return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_iet_livery_decider, livery_choice){
	1: sw_iet_refit_text;
	return CB_RESULT_NO_TEXT;
}

random_switch(FEAT_TRAINS, SELF, sw_iet_hitachi_colour_mapping, TRIGGER_VEHICLE_SERVICE){1: palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;}
random_switch(FEAT_TRAINS, SELF, sw_iet_gwr_colour_mapping, TRIGGER_VEHICLE_SERVICE){1: palette_2cc(COLOUR_DARK_GREEN, COLOUR_GREY) ;}
random_switch(FEAT_TRAINS, SELF, sw_iet_tpe_colour_mapping, TRIGGER_VEHICLE_SERVICE){1: palette_2cc(COLOUR_DARK_BLUE, COLOUR_PINK) ;}
random_switch(FEAT_TRAINS, SELF, sw_iet_ec_colour_mapping, TRIGGER_VEHICLE_SERVICE){1: palette_2cc(COLOUR_WHITE, COLOUR_RED) ;}

switch(FEAT_TRAINS, SELF, sw_iet_colour_auto, current_year){
sw_iet_hitachi_colour_mapping;
}

switch(FEAT_TRAINS, SELF, sw_iet_colour_man, cargo_subtype){
	0: sw_iet_hitachi_colour_mapping;
	1: sw_iet_gwr_colour_mapping;
	2..3: sw_iet_ec_colour_mapping;
	sw_iet_tpe_colour_mapping;
}

switch(FEAT_TRAINS, SELF, sw_iet_colour_livery_decider, livery_choice){
	1: sw_iet_colour_man;
	sw_iet_colour_auto;
}

switch(FEAT_TRAINS, SELF, sw_iet_recolour, colour_scheme_1){
	1: sw_iet_colour_livery_decider;
	palette_2cc(company_colour1, company_colour2);
}

switch(FEAT_TRAINS, SELF, sw_iet_coupling_restrictions, vehicle_type_id) {
    9090: return CB_RESULT_ATTACH_ALLOW;
	800: return CB_RESULT_ATTACH_ALLOW;
	37 : return CB_RESULT_ATTACH_ALLOW;
    return string(STR_IET_NO_COUPLE);
}

switch(FEAT_TRAINS, SELF, sw_iet_speed, current_railtype) {
    ELRL: 225;
    SAAZ: 225;
	200;
}

switch(FEAT_TRAINS, SELF, sw_iet_create_effect, current_railtype){
	ELRL   : vfx_ohle ;
	SAAZ   : vfx_ohle ;
	vfx_diesel_default ;
}

switch(FEAT_TRAINS, SELF, sw_iet_t_create_effect, current_railtype){
	ELRL   : 0 ;
	SAAZ   : 0 ;
	vfx_diesel_default ;
}

switch(FEAT_TRAINS, SELF, sw_iet_te, current_railtype){
	ELRL   : 0.55 ;
	SAAZ   : 0.55 ;
	0.35 ;
}


item (FEAT_TRAINS, item_800, 800) {
	property {
		name: 							string(STR_NAME_800);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(2016, 1, 1);
		model_life:						20;
		retire_early:					1;
		vehicle_life:					30;
		reliability_decay:				7;
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		cargo_allow_refit:				[PASS, TOUR];
		loading_speed:					20;
		cost_factor:					60;
		running_cost_factor:			35;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							140 mph;
		misc_flags:						bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		refit_cost:						0;
		track_type:						RAIL;
		ai_special_flag:				AI_FLAG_CARGO;
		power:							1120 hp;
		running_cost_base:				RUNNING_COST_ELECTRIC;
		dual_headed:					1;
		default_cargo_type:				PASS;
		cargo_capacity:					40;
		weight:							47 ton;
		tractive_effort_coefficient:	0.25;
		air_drag_coefficient:			0.07;
		length:							8;
		effect_spawn_model_and_powered:	EFFECT_SPAWN_MODEL_ELECTRIC;
		bitmask_vehicle_info:			0;
		cargo_age_period:				250;
		default_cargo_type:				PASS;
	}
	graphics {
		can_attach_wagon:					sw_iet_coupling_restrictions;
		default: 							sw_iet_default;
		purchase:							spriteset_iet_purchase;
		cargo_subtype_text:					sw_iet_livery_decider ;
		colour_mapping:						sw_iet_recolour ;
		purchase_colour_mapping:			sw_iet_recolour ;
		sound_effect:						sw_sfx_electric_loco_modern ;
		speed:								sw_iet_speed;
		running_cost_factor:				return(current_speed == 0 ? 8 : 35) ;
		purchase_running_cost_factor : 35;
		start_stop:							sw_iet_start_stop;
		create_effect:						sw_iet_create_effect;
		tractive_effort_coefficient:	sw_iet_te;
}
		livery_override (item_MU90) {
		cargo_capacity: 66;
		default: sw_iet_t;
		colour_mapping:	sw_iet_recolour;
		cargo_subtype_text: sw_iet_livery_decider ;
		weight:							45;
		power:							560;
		create_effect:						sw_iet_t_create_effect;
}
}

if (vehicle_selection < 2){
item (FEAT_TRAINS, item_800, 800){
	property{
		climates_available             : NO_CLIMATE ;
	}
}
}

if (alt_names == 1){
item (FEAT_TRAINS, item_800, 800){
	property{
		name                           : string(STR_NAME_800_ALT) ;
	}
}
}