//Graphics

template template_btrsea(x, y){ [ x  , y  , 64 , 122, -31 , -91] }

spriteset(gfx_btrsea_1_0       , "gfx/battersea_power_station.png"){ template_btrsea( 0  , 0  ) }
spriteset(gfx_btrsea_2_0       , "gfx/battersea_power_station.png"){ template_btrsea( 65 , 0  ) }
spriteset(gfx_btrsea_3_0       , "gfx/battersea_power_station.png"){ [ 130, 0  , 64 , 122, -33 , -92] }
spriteset(gfx_btrsea_1_1       , "gfx/battersea_power_station.png"){ template_btrsea( 195, 0  ) }
spriteset(gfx_btrsea_2_1       , "gfx/battersea_power_station.png"){ template_btrsea( 260, 0  ) }
spriteset(gfx_btrsea_3_1       , "gfx/battersea_power_station.png"){ template_btrsea( 325, 0  ) }

spriteset(gfx_btrsea_3_0_pig_1 , "gfx/battersea_power_station.png"){ [ 0  , 123, 71 , 134, -33 , -104] }
spriteset(gfx_btrsea_3_0_pig_2 , "gfx/battersea_power_station.png"){ [ 72 , 123, 71 , 134, -33 , -104] }
spriteset(gfx_btrsea_3_0_pig_3 , "gfx/battersea_power_station.png"){ [ 144, 123, 71 , 134, -33 , -104] }
spriteset(gfx_btrsea_3_0_pig_4 , "gfx/battersea_power_station.png"){ [ 216, 123, 71 , 134, -33 , -104] }

spriteset(gfx_btrsea_1_0_1     , "gfx/battersea_power_station.png"){ [ 0  , 258, 92 , 147, -31 , -116] }  
spriteset(gfx_btrsea_1_0_2     , "gfx/battersea_power_station.png"){ [ 93 , 258, 92 , 147, -31 , -116] }  
spriteset(gfx_btrsea_1_0_3     , "gfx/battersea_power_station.png"){ [ 186, 258, 92 , 147, -31 , -116] }  
spriteset(gfx_btrsea_1_0_4     , "gfx/battersea_power_station.png"){ [ 279, 258, 92 , 147, -31 , -116] }  

spriteset(gfx_btrsea_3_0_1     , "gfx/battersea_power_station.png"){ [ 0  , 406, 72 , 135, -33 , -105] }
spriteset(gfx_btrsea_3_0_2     , "gfx/battersea_power_station.png"){ [ 73 , 406, 72 , 135, -33 , -105] }
spriteset(gfx_btrsea_3_0_3     , "gfx/battersea_power_station.png"){ [ 146, 406, 72 , 135, -33 , -105] }
spriteset(gfx_btrsea_3_0_4     , "gfx/battersea_power_station.png"){ [ 219, 406, 72 , 135, -33 , -105] }

spriteset(gfx_btrsea_1_1_1     , "gfx/battersea_power_station.png"){ [ 0  , 542, 84 , 152, -31 , -121] }  
spriteset(gfx_btrsea_1_1_2     , "gfx/battersea_power_station.png"){ [ 85 , 542, 84 , 152, -31 , -121] }    
spriteset(gfx_btrsea_1_1_3     , "gfx/battersea_power_station.png"){ [ 170, 542, 84 , 152, -31 , -121] }    
spriteset(gfx_btrsea_1_1_4     , "gfx/battersea_power_station.png"){ [ 255, 542, 84 , 152, -31 , -121] }    

spriteset(gfx_btrsea_3_1_1     , "gfx/battersea_power_station.png"){ [ 0  , 695, 64 , 140, -31 , -109] }  
spriteset(gfx_btrsea_3_1_2     , "gfx/battersea_power_station.png"){ [ 65 , 695, 64 , 140, -31 , -109] }    
spriteset(gfx_btrsea_3_1_3     , "gfx/battersea_power_station.png"){ [ 130, 695, 64 , 140, -31 , -109] }    
spriteset(gfx_btrsea_3_1_4     , "gfx/battersea_power_station.png"){ [ 195, 695, 64 , 140, -31 , -109] }    

spriteset(gfx_btrsea_coal_med  , "gfx/battersea_power_station.png"){ [ 390, 0  , 64 , 34 , -31 , -3  ] } 
spriteset(gfx_btrsea_coal_full , "gfx/battersea_power_station.png"){ [ 455, 0  , 64 , 34 , -31 , -3  ] } 
spriteset(gfx_btrsea_tank      , "gfx/battersea_power_station.png"){ [ 390 , 35, 64 , 44 , -31 , -13 ] } 



//Sprite layouts

spritelayout spritelayout_btrsea_1_0   	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_1_0 ;       zextent : 80 ;} }
spritelayout spritelayout_btrsea_1_0_1 	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_1_0_1 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_1_0_2 	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_1_0_2 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_1_0_3 	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_1_0_3 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_1_0_4 	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_1_0_4 ;     zextent : 80 ;} }

spritelayout spritelayout_btrsea_2_0 	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_2_0 ;       zextent : 80 ;} }

spritelayout spritelayout_btrsea_3_0 	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_0 ;       zextent : 80 ;} }
spritelayout spritelayout_btrsea_3_0_1	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_0_1 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_3_0_2	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_0_2 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_3_0_3	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_0_3 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_3_0_4	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_0_4 ;     zextent : 80 ;} }

spritelayout spritelayout_btrsea_1_1 	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_1_1 ;       zextent : 80 ;} }
spritelayout spritelayout_btrsea_1_1_1	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_1_1_1 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_1_1_2	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_1_1_2 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_1_1_3	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_1_1_3 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_1_1_4	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_1_1_4 ;     zextent : 80 ;} }

spritelayout spritelayout_btrsea_2_1	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_2_1 ;       zextent : 80 ;} }

spritelayout spritelayout_btrsea_3_1 	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_1 ;       zextent : 80 ;} }
spritelayout spritelayout_btrsea_3_1_1	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_1_1 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_3_1_2	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_1_2 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_3_1_3	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_1_3 ;     zextent : 80 ;} }
spritelayout spritelayout_btrsea_3_1_4	   { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_1_4 ;     zextent : 80 ;} }
	
spritelayout spritelayout_btrsea_3_0_pig_1 { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_0_pig_1 ; zextent : 80 ;} }
spritelayout spritelayout_btrsea_3_0_pig_2 { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_0_pig_2 ; zextent : 80 ;} }
spritelayout spritelayout_btrsea_3_0_pig_3 { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_0_pig_3 ; zextent : 80 ;} }
spritelayout spritelayout_btrsea_3_0_pig_4 { ground { sprite : gfx_nucrep_empty_tile ; } building { sprite : gfx_btrsea_3_0_pig_4 ; zextent : 80 ;} }

spritelayout spritelayout_btrsea_coal_empty  { ground { sprite : gfx_fence_ne ; } }
spritelayout spritelayout_btrsea_coal_med  { ground { sprite : gfx_fence_ne ; } building { sprite : gfx_btrsea_coal_med ; } }
spritelayout spritelayout_btrsea_coal_full { ground { sprite : gfx_fence_ne ; } building { sprite : gfx_btrsea_coal_full ; } }
spritelayout spritelayout_btrsea_tank      { ground { sprite : gfx_fence_n ; } building { sprite : gfx_btrsea_tank ; } }


spritelayout spritelayout_btrsea_4_0 { ground { sprite : gfx_fence_w  ; } }
spritelayout spritelayout_btrsea_4_1 { ground { sprite : gfx_fence_sw ; } }
spritelayout spritelayout_btrsea_0_2 { ground { sprite : gfx_fence_e  ; } }
spritelayout spritelayout_btrsea_1_2 { ground { sprite : gfx_fence_se ; } }
spritelayout spritelayout_btrsea_4_2 { ground { sprite : gfx_fence_s  ; } }
//Tiles

//Base



switch(FEAT_INDUSTRYTILES, SELF, btrsea_empty_gfx_decider, relative_pos){
	relative_coord(4,0) : spritelayout_btrsea_4_0 ;
	relative_coord(4,1) : spritelayout_btrsea_4_1 ;
	relative_coord(0,2) : spritelayout_btrsea_0_2 ;
	relative_coord(1,2) : spritelayout_btrsea_1_2 ;
	relative_coord(2,2) : spritelayout_btrsea_1_2 ;
	relative_coord(3,2) : spritelayout_btrsea_1_2 ;
	relative_coord(4,2) : spritelayout_btrsea_4_2 ;
}


//Animations

switch(FEAT_INDUSTRYTILES, SELF, sw_btrsea_pig_animation, animation_frame){
	0 : spritelayout_btrsea_3_0_pig_1 ;
	1 : spritelayout_btrsea_3_0_pig_2 ;
	2 : spritelayout_btrsea_3_0_pig_3 ;
	spritelayout_btrsea_3_0_pig_4 ;
}

switch(FEAT_INDUSTRYTILES, SELF, sw_btrsea_1_0_animation, animation_frame){
	0 : spritelayout_btrsea_1_0_1 ;
	1 : spritelayout_btrsea_1_0_2 ;
	2 : spritelayout_btrsea_1_0_3 ;
	spritelayout_btrsea_1_0_4 ;
}

switch(FEAT_INDUSTRYTILES, SELF, sw_btrsea_3_0_animation, animation_frame){
	0 : spritelayout_btrsea_3_0_1 ;
	1 : spritelayout_btrsea_3_0_2 ;
	2 : spritelayout_btrsea_3_0_3 ;
	spritelayout_btrsea_3_0_4 ;
}

switch(FEAT_INDUSTRYTILES, SELF, sw_btrsea_1_1_animation, animation_frame){
	0 : spritelayout_btrsea_1_1_1 ;
	1 : spritelayout_btrsea_1_1_2 ;
	2 : spritelayout_btrsea_1_1_3 ;
	spritelayout_btrsea_1_1_4 ;
}

switch(FEAT_INDUSTRYTILES, SELF, sw_btrsea_3_1_animation, animation_frame){
	0 : spritelayout_btrsea_3_1_1 ;
	1 : spritelayout_btrsea_3_1_2 ;
	2 : spritelayout_btrsea_3_1_3 ;
	spritelayout_btrsea_3_1_4 ;
}


//Without pig

switch(FEAT_INDUSTRYTILES, SELF, btrsea_chimney_gfx_non_animated, relative_pos){
	relative_coord(1, 0) : spritelayout_btrsea_1_0 ;
	relative_coord(3, 0) : spritelayout_btrsea_3_0 ;
	relative_coord(1, 1) : spritelayout_btrsea_1_1 ;
	relative_coord(3, 1) : spritelayout_btrsea_3_1 ;
	spritelayout_btrsea_1_0 ;
}

switch(FEAT_INDUSTRYTILES, SELF, btrsea_chimney_gfx_animated, relative_pos){
	relative_coord(1, 0) : sw_btrsea_1_0_animation ;
	relative_coord(3, 0) : sw_btrsea_3_0_animation ;
	relative_coord(1, 1) : spritelayout_btrsea_1_1 ;
	relative_coord(3, 1) : sw_btrsea_3_1_animation ;
	sw_btrsea_1_0_animation ;
}

switch(FEAT_INDUSTRYTILES, PARENT, btrsea_chimney_gfx_decider, waiting_cargo_1 + waiting_cargo_2){
	0 : btrsea_chimney_gfx_non_animated ;
	btrsea_chimney_gfx_animated ;
}


//With pig

switch(FEAT_INDUSTRYTILES, SELF, btrsea_chimney_gfx_non_animated_pig, relative_pos){
	relative_coord(1, 0) : spritelayout_btrsea_1_0 ;
	relative_coord(3, 0) : sw_btrsea_pig_animation ;
	relative_coord(1, 1) : spritelayout_btrsea_1_1 ;
	relative_coord(3, 1) : spritelayout_btrsea_3_1 ;
	spritelayout_btrsea_1_0 ;
}

switch(FEAT_INDUSTRYTILES, SELF, btrsea_chimney_gfx_animated_pig, relative_pos){
	relative_coord(1, 0) : sw_btrsea_1_0_animation ;
	relative_coord(3, 0) : sw_btrsea_pig_animation ;
	relative_coord(1, 1) : spritelayout_btrsea_1_1 ;
	relative_coord(3, 1) : sw_btrsea_3_1_animation ;
	sw_btrsea_1_0_animation ;
}

switch(FEAT_INDUSTRYTILES, PARENT, btrsea_chimney_gfx_decider_pig, waiting_cargo_1 + waiting_cargo_2){
	0 : btrsea_chimney_gfx_non_animated_pig ;
	btrsea_chimney_gfx_animated_pig ;
}



//Pig check

random_switch(FEAT_INDUSTRYTILES, SELF, btrsea_chimney_random_pig_check){
	2 : btrsea_chimney_gfx_decider ;
	1 : btrsea_chimney_gfx_decider_pig ;
}



item(FEAT_INDUSTRYTILES, btrsea_chimney_tile){
	property{
		substitute         : 0x07 ;
		accepted_cargos    : [] ;
		land_shape_flags   : bitmask(LSF_ONLY_ON_FLAT_LAND) ;
		animation_info     : [ANIMATION_LOOPING, 4] ;
		animation_speed    : 2 ;
		animation_triggers : bitmask(ANIM_TRIGGER_INDTILE_TILE_LOOP) ;
	}
	graphics{
		default         : btrsea_chimney_random_pig_check ;
		anim_next_frame : nucrep_chimney_animation ;
	}
}


item(FEAT_INDUSTRYTILES, btrsea_empty){
	property{
		substitute         : 0x08 ;
		accepted_cargos    : [] ;
		land_shape_flags   : bitmask(LSF_ONLY_ON_FLAT_LAND) ;
	}
	graphics{
		default         : btrsea_empty_gfx_decider ;
	}
}



switch(FEAT_INDUSTRYTILES, PARENT, sw_btrsea_coal_decider, waiting_cargo_1){
	1..250     : spritelayout_btrsea_coal_med ;
	250..65536 : spritelayout_btrsea_coal_full ;
	spritelayout_btrsea_coal_empty ;
}

switch(FEAT_INDUSTRYTILES, SELF, btrsea_building_gfx_decider, relative_pos){
	relative_coord(0, 0) : spritelayout_btrsea_tank ;
	relative_coord(0, 1) : sw_btrsea_coal_decider ;
	relative_coord(2, 0) : spritelayout_btrsea_2_0 ;
	relative_coord(2, 1) : spritelayout_btrsea_2_1 ;
	spritelayout_btrsea_1_1 ;
}

item (FEAT_INDUSTRYTILES, btrsea_building_tile){
	property{
		substitute       : 0x08 ;
		accepted_cargos  : [[COAL, 8],[OIL_, 8],[URAN, 8]] ;
		land_shape_flags : bitmask(LSF_ONLY_ON_FLAT_LAND) ;
	}
	graphics{
		default    : btrsea_building_gfx_decider ;
	}
}


//Tile layout

tilelayout btrsea_tilelayout {
	0, 0 : btrsea_building_tile ;
	1, 0 : btrsea_chimney_tile ;
	2, 0 : btrsea_building_tile ;
	3, 0 : btrsea_chimney_tile ;
	4, 0 : btrsea_empty ;
	0, 1 : btrsea_building_tile ;
	1, 1 : btrsea_chimney_tile ;
	2, 1 : btrsea_building_tile ;
	3, 1 : btrsea_chimney_tile ;
	4, 1 : btrsea_empty ;
	0, 2 : btrsea_empty ;
	1, 2 : btrsea_empty ;
	2, 2 : btrsea_empty ;
	3, 2 : btrsea_empty ;
	4, 2 : btrsea_empty ;
}


produce(produce_btrsea_none   , 68 , 68 , 4 , 0 , 0);
produce(produce_btrsea_low    , 60 , 60 , 6, 4 , 0);
produce(produce_btrsea_medium , 52 , 52 , 9, 6 , 0);
produce(produce_btrsea_high   , 48 , 48 , 15, 8, 0);
produce(produce_btrsea_arrival, 150, 150, 15 ,9 , 0);

switch(FEAT_INDUSTRIES, SELF, sw_btrsea_produce, waiting_cargo_3){
	0       : produce_btrsea_none ;
	1..40   : produce_btrsea_low ;
	41..80  : produce_btrsea_medium ;
	produce_btrsea_high ;
}

if(nuclear_enabled == 1){
item(FEAT_INDUSTRIES, btrsea_power_station){
	property {
		substitute            : 0x01 ;
		layouts               : [btrsea_tilelayout] ;
		life_type             : IND_LIFE_TYPE_PROCESSING ;
		accept_cargo_types    : [cargotype("COAL"), cargotype("OIL_"), cargotype("URAN")];
		conflicting_ind_types : [industry_type(IND_TYPE_OLD, 0x01), industry_type(IND_TYPE_NEW, 0), industry_type(IND_TYPE_NEW, 1)] ;
		prod_cargo_types      : [cargotype("NWST")] ;
		nearby_station_name   : string(STR_POWER_STATION_STATION_NAME) ;
		prob_map_gen          : 4 ;
		prob_in_game          : 1 ;
	}	
	graphics {
		//produce_cargo_arrival : produce_btrsea_arrival ;
		produce_256_ticks     : sw_btrsea_produce ;
	}
}
}